#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.РазмерДанных = РазмерДанных(Запись.ВерсияОбъекта);
		
		ДанныеВерсии = Запись.ВерсияОбъекта.Получить();
		Запись.ЕстьДанныеВерсии = ДанныеВерсии <> Неопределено;
		
		Если ПустаяСтрока(Запись.КонтрольнаяСумма) И Запись.ЕстьДанныеВерсии Тогда
			Если ТипЗнч(ДанныеВерсии) = Тип("Структура") Тогда
				ВерсияОбъекта = ДанныеВерсии.Объект;
			Иначе
				ВерсияОбъекта = ДанныеВерсии;
			КонецЕсли;
			Запись.КонтрольнаяСумма = ВерсионированиеОбъектов.КонтрольнаяСумма(ВерсияОбъекта);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазмерДанных(Данные)
	Возврат Base64Значение(СериализаторXDTO.XMLСтрока(Данные)).Размер();
КонецФункции

#КонецОбласти

#КонецЕсли