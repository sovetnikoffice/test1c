////////////////////////////////////////////////////////////////////////////////
// Подсистема "Оценка производительности".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Функция определяет необходимость выполнения замеров.
//
// Возвращаемое значение:
//  Булево - Истина выполнять, Ложь не выполнять.
//
Функция ВыполнятьЗамерыПроизводительности() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.ВыполнятьЗамерыПроизводительности.Получить();
	
КонецФункции

// Функция возвращает ссылку на ключевую операцию по имени.
// Если ключевая операция с таким названием отсутствует в справочнике, 
// то создает новый элемент.
//
// Параметры:
//  ИмяКлючевойОперации - Строка - название ключевой операции
//
// Возвращаемое значение:
//	СправочникСсылка.КлючевыеОперации
//
Функция ПолучитьКлючевуюОперациюПоИмени(ИмяКлючевойОперации) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		           |	КлючевыеОперации.Ссылка КАК Ссылка
		           |ИЗ
		           |	Справочник.КлючевыеОперации КАК КлючевыеОперации
		           |ГДЕ
		           |	КлючевыеОперации.ИмяХеш = &ИмяХеш
		           |
		           |УПОРЯДОЧИТЬ ПО
		           |	Ссылка";
	
	ХешMD5 = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешMD5.Добавить(ИмяКлючевойОперации);
	ИмяХеш = ХешMD5.ХешСумма;
	ИмяХеш = СтрЗаменить(Строка(ИмяХеш), " ", "");			   
					   
	Запрос.УстановитьПараметр("ИмяХеш", ИмяХеш);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		КлючеваяОперацияСсылка = ОценкаПроизводительностиВызовСервераПолныеПрава.СоздатьКлючевуюОперацию(ИмяКлючевойОперации);
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		КлючеваяОперацияСсылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат КлючеваяОперацияСсылка;
	
КонецФункции

#КонецОбласти
