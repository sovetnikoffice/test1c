////////////////////////////////////////////////////////////////////////////////
// Подсистема "Версионирование объектов".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Выполняется при обновлении конфигурации.
// 1. Очищает настройки версионирования по объектам, для которых версионирование не применено.
// 2. Устанавливает настройки версионирования по умолчанию.
//
Процедура ОбновитьНастройкиВерсионированияОбъектов() Экспорт
	
	ВерсионируемыеОбъекты = ПолучитьВерсионируемыеОбъекты();
	
	ВыборкаЗаписей = РегистрыСведений.НастройкиВерсионированияОбъектов.Выбрать();
	Пока ВыборкаЗаписей.Следующий() Цикл
		Если ВерсионируемыеОбъекты.Найти(ВыборкаЗаписей.ТипОбъекта) = Неопределено Тогда
			МенеджерЗаписи = ВыборкаЗаписей.ПолучитьМенеджерЗаписи();
			МенеджерЗаписи.Удалить();
		КонецЕсли;
	КонецЦикла;
	
	ВерсионируемыеОбъектыТЗ = Новый ТаблицаЗначений;
	ВерсионируемыеОбъектыТЗ.Колонки.Добавить("ТипОбъекта", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	Для Каждого ТипОбъекта Из ВерсионируемыеОбъекты Цикл
		ВерсионируемыеОбъектыТЗ.Добавить();
	КонецЦикла;
	ВерсионируемыеОбъектыТЗ.ЗагрузитьКолонку(ВерсионируемыеОбъекты, "ТипОбъекта");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВерсионируемыеОбъекты.ТипОбъекта
	|ПОМЕСТИТЬ ТаблицаВерсионируемыхОбъектов
	|ИЗ
	|	&ВерсионируемыеОбъекты КАК ВерсионируемыеОбъекты
	|;
	|////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВерсионируемыхОбъектов.ТипОбъекта
	|ИЗ
	|	ТаблицаВерсионируемыхОбъектов КАК ТаблицаВерсионируемыхОбъектов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВерсионированияОбъектов КАК НастройкиВерсионированияОбъектов
	|			ПО НастройкиВерсионированияОбъектов.ТипОбъекта = ТаблицаВерсионируемыхОбъектов.ТипОбъекта
	|ГДЕ
	|	НастройкиВерсионированияОбъектов.Вариант ЕСТЬ NULL ";
			
	Запрос.Параметры.Вставить("ВерсионируемыеОбъекты", ВерсионируемыеОбъектыТЗ);
	ВерсионируемыеОбъектыБезНастройки = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТипОбъекта");
	
	НаборЗаписейНастроек = РегистрыСведений.НастройкиВерсионированияОбъектов.СоздатьНаборЗаписей();
	НаборЗаписейНастроек.Прочитать();
	Для Каждого ВерсионируемыйОбъект Из ВерсионируемыеОбъектыБезНастройки Цикл
		НоваяЗапись = НаборЗаписейНастроек.Добавить();
		НоваяЗапись.ТипОбъекта = ВерсионируемыйОбъект;
		НоваяЗапись.Вариант = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать;
	КонецЦикла;
	
	НаборЗаписейНастроек.Записать(Истина);
	
КонецПроцедуры

// Записывает настройку версионирования объекта.
//
// Параметры:
//  ТипОбъекта - Строка, Тип, ОбъектМетаданных, СправочникСсылка.ИдентификаторыОбъектовМетаданных - объект метаданных;
//  ВариантВерсионирования - ПеречислениеСсылка.ВариантыВерсионированияОбъектов - условие записи версий;
//  СрокХраненияВерсий - ПеречислениеСсылка.СрокиХраненияВерсий - срок, после которого версии подлежат очистке.
//
Процедура ЗаписатьНастройкуВерсионированияПоОбъекту(Знач ТипОбъекта, Знач ВариантВерсионирования, Знач СрокХраненияВерсий = Неопределено) Экспорт
	
	Если ТипЗнч(ТипОбъекта) <> Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
		ТипОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ТипОбъекта);
	КонецЕсли;
	
	Настройка = РегистрыСведений.НастройкиВерсионированияОбъектов.СоздатьМенеджерЗаписи();
	Настройка.ТипОбъекта = ТипОбъекта;
	
	Если СрокХраненияВерсий = Неопределено Тогда
		Настройка.Прочитать();
		Если Настройка.Выбран() Тогда
			СрокХраненияВерсий = Настройка.СрокХраненияВерсий;
		Иначе
			Настройка.ТипОбъекта = ТипОбъекта;
			СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.Бессрочно;
		КонецЕсли;
	КонецЕсли;
	
	Настройка.СрокХраненияВерсий = СрокХраненияВерсий;
	Настройка.Вариант = ВариантВерсионирования;
	Настройка.Записать();
	
КонецПроцедуры

// Выполняет с формой действия, необходимые для подключения подсистемы версионирования.
//
// Параметры:
//  Форма - УправляемаяФорма - форма для подключения механизма версионирования.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	ПолноеИмяМетаданных = Неопределено;
	Если Пользователи.РолиДоступны("ЧтениеВерсийОбъектов") И ПолучитьФункциональнуюОпцию("ИспользоватьВерсионированиеОбъектов") Тогда
		ИмяФормыМассив = СтрРазделить(Форма.ИмяФормы, ".", Ложь);
		ПолноеИмяМетаданных = ИмяФормыМассив[0] + "." + ИмяФормыМассив[1];
	КонецЕсли;
	
	Объект = Неопределено;
	Если ПолноеИмяМетаданных <> Неопределено Тогда
		Объект = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяМетаданных);
	КонецЕсли;
	
	Форма.УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("ТипВерсионируемогоОбъекта", Объект));
	
КонецПроцедуры

// Возвращает табличный документ, заполненный данными объекта.
// 
// Параметры:
//  СсылкаНаОбъект - ЛюбаяСсылка.
//
// Возвращаемое значение:
//  ТабличныйДокумент - печатная форма объекта.
//
Функция ОтчетПоВерсииОбъекта(СсылкаНаОбъект, Знач ВерсияОбъекта = Неопределено) Экспорт
	
	НомерВерсии = Неопределено;
	СериализованныйОбъект = Неопределено;
	Если ТипЗнч(ВерсияОбъекта) = Тип("Число") Тогда
		НомерВерсии = ВерсияОбъекта;
	ИначеЕсли ТипЗнч(ВерсияОбъекта) = Тип("Строка") Тогда
		СериализованныйОбъект = ВерсияОбъекта;
	КонецЕсли;
	
	Если НомерВерсии = Неопределено Тогда
		Если СериализованныйОбъект = Неопределено Тогда
			СериализованныйОбъект = СериализоватьОбъект(СсылкаНаОбъект.ПолучитьОбъект());
		КонецЕсли;
		ОписаниеОбъекта = РазборПредставленияОбъектаXML(СериализованныйОбъект, СсылкаНаОбъект);
		ОписаниеОбъекта.Вставить("ИмяОбъекта",     Строка(СсылкаНаОбъект));
		ОписаниеОбъекта.Вставить("АвторИзменения", "");
		ОписаниеОбъекта.Вставить("ДатаИзменения",  ТекущаяДатаСеанса());
		ОписаниеОбъекта.Вставить("Комментарий", "");
		НомерВерсии = 0;
		
		ВерсионированиеОбъектовПереопределяемый.ПослеРазбораВерсииОбъекта(СсылкаНаОбъект, ОписаниеОбъекта);
	Иначе
		ОписаниеОбъекта = РазборВерсии(СсылкаНаОбъект, НомерВерсии);
	КонецЕсли;
	
	Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '№ %1 / (%2) / %3'"), НомерВерсии,
		Строка(ОписаниеОбъекта.ДатаИзменения), СокрЛП(Строка(ОписаниеОбъекта.АвторИзменения)));
		
	ОписаниеОбъекта.Вставить("Описание", Описание);
	ОписаниеОбъекта.Вставить("НомерВерсии", НомерВерсии);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	СформироватьОтчетПоВерсииОбъекта(ТабличныйДокумент, ОписаниеОбъекта, СсылкаНаОбъект);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииОбработчиковСлужебныхСобытий(КлиентскиеОбработчики, СерверныеОбработчики) Экспорт
	
	// СЕРВЕРНЫЕ ОБРАБОТЧИКИ.
	
	СерверныеОбработчики["СтандартныеПодсистемы.БазоваяФункциональность\ПриДобавленииИсключенийПоискаСсылок"].Добавить(
		"ВерсионированиеОбъектов");
		
	СерверныеОбработчики["СтандартныеПодсистемы.БазоваяФункциональность\ПриПолученииДанныхОтПодчиненного"].Добавить(
		"ВерсионированиеОбъектов");
	
	СерверныеОбработчики["СтандартныеПодсистемы.БазоваяФункциональность\ПриПолученииДанныхОтГлавного"].Добавить(
		"ВерсионированиеОбъектов");
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ТекущиеДела") Тогда
		СерверныеОбработчики["СтандартныеПодсистемы.ТекущиеДела\ПриЗаполненииСпискаТекущихДел"].Добавить(
			"ВерсионированиеОбъектов");
	КонецЕсли;
	
	СерверныеОбработчики["СтандартныеПодсистемы.ОбновлениеВерсииИБ\ПриДобавленииОбработчиковОбновления"].Добавить(
		"ВерсионированиеОбъектов");
		
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий") Тогда
		СерверныеОбработчики[
			"СтандартныеПодсистемы.РаботаВМоделиСервиса.ОчередьЗаданий\ПриОпределенииПсевдонимовОбработчиков"].Добавить(
			"ВерсионированиеОбъектов");
	КонецЕсли;
		
КонецПроцедуры

// Добавляет процедуры-обработчики обновления, необходимые данной подсистеме.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                  общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.2.3.8";
	Обработчик.Процедура = "ВерсионированиеОбъектов.ОбновитьСведенияОВерсияхОбъектов";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("b403067e-7c2d-461c-89f9-5d5fc95dcab6");
	Обработчик.Комментарий = НСтр("ru = 'Обновление сведений о записанных версиях объектов. После выполнения обновления увеличится скорость записи версионируемых объектов.'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "2.2.4.13";
	Обработчик.Процедура = "ВерсионированиеОбъектов.ПеренестиНастройкиВерсионирования";
	Обработчик.Комментарий = НСтр("ru = 'Обновление настроек версионирования.'");
	
КонецПроцедуры

// Дозаполняет реквизиты регистра версий.
Процедура ОбновитьСведенияОВерсияхОбъектов(Параметры) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 10000
	|	ВерсииОбъектов.Объект,
	|	ВерсииОбъектов.НомерВерсии
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.РазмерДанных = 0";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОбработаноЗаписей = 0;
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		НаборЗаписей.Отбор.НомерВерсии.Установить(Выборка.НомерВерсии);
		НаборЗаписей.Прочитать();
		Попытка
			НаборЗаписей.Записать();
			ОбработаноЗаписей = ОбработаноЗаписей + 1;
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Версионирование'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, НаборЗаписей.Метаданные(),
				,
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Не удалось обновить сведения о версии №%1 объекта ""%2"" по причине:
					|%3'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
					Выборка.НомерВерсии,
					ОбщегоНазначения.ПредметСтрокой(Выборка.Объект),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		КонецПопытки;
	КонецЦикла;
	
	Если Выборка.Количество() > 0 Тогда
		Если ОбработаноЗаписей = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Процедуре ОбновитьСведенияОВерсияхОбъектов не удалось обработать некоторые записи регистра сведений ВерсииОбъектов (пропущены): %1'"), 
					Выборка.Количество());
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		Параметры.ОбработкаЗавершена = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Записывает версию объекта в информационную базу.
//
// Параметры:
//	Объект - для создания версии.
//
//Процедура ПриСозданииВерсииОбъекта(Объект, РежимЗаписиПроведение) Экспорт
Процедура ПриСозданииВерсииОбъекта(Объект, РежимЗаписи) Экспорт
	
	Перем НомерПоследнейВерсии, Комментарий;
	
	Если НЕ ОбъектВерсионируется(Объект, НомерПоследнейВерсии, РежимЗаписи = РежимЗаписиДокумента.Проведение
		Или РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) Тогда
			Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ДополнительныеСвойства.Свойство("ВерсионированиеОбъектовКомментарийКВерсии", Комментарий) Тогда
		Комментарий = "";
	КонецЕсли;
	
	СведенияОВерсииОбъекта = Новый Структура;
	СведенияОВерсииОбъекта.Вставить("НомерВерсии", Число(НомерПоследнейВерсии) + 1);
	СведенияОВерсииОбъекта.Вставить("Комментарий", Комментарий);
	
	ИзмененаПроведенность = (РежимЗаписи = РежимЗаписиДокумента.Проведение И Не Объект.Проведен
		Или РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения И Объект.Проведен);
	СведенияОВерсииОбъекта.Вставить("ИзмененаПроведенность", ИзмененаПроведенность);
	
	СоздатьВерсиюОбъекта(Объект, СведенияОВерсииОбъекта);
	
КонецПроцедуры

// Записывает версию объекта, полученного при обмене данными, в информационную базу.
// Для версии объекта не по коллизии проверяет включенность версионирования.
//
// Параметры:
//	Объект - для создания версии.
//
Процедура ПриСозданииВерсииОбъектаПоОбменуДанными(Объект) Экспорт
	
	Перем НомерПоследнейВерсии;
	
	Если Не ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(Объект.Метаданные()) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Ссылка = Объект.Ссылка;
	
	СведенияОВерсииОбъекта = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(
		Объект.ДополнительныеСвойства.СведенияОВерсииОбъекта);
	
	Если СведенияОВерсииОбъекта.ТипВерсииОбъекта = "ПринятыеДанныеПоКоллизии" Тогда
		
		НомерПоследнейВерсии = НомерПоследнейВерсии(Ссылка);
		
		СведенияОВерсииОбъекта.Вставить("Объект", Ссылка);
		СведенияОВерсииОбъекта.Вставить("НомерВерсии", Число(НомерПоследнейВерсии) + 1);
		СведенияОВерсииОбъекта.ТипВерсииОбъекта = Перечисления.ТипыВерсийОбъекта[СведенияОВерсииОбъекта.ТипВерсииОбъекта];
		
		СоздатьВерсиюОбъекта(Объект, СведенияОВерсииОбъекта, Ложь);
		
	ИначеЕсли ОбъектВерсионируется(Объект, НомерПоследнейВерсии) Тогда
		
		Если СведенияОВерсииОбъекта.ОтложеннаяОбработка Тогда
			
			НомерПоследнейВерсии = НомерПоследнейВерсии(Ссылка);
			ЗаписатьВерсиюПоверхПредыдущей(Ссылка, Объект, НомерПоследнейВерсии, СведенияОВерсииОбъекта);
			
		Иначе
			
			СведенияОВерсииОбъекта.Вставить("Объект", Ссылка);
			СведенияОВерсииОбъекта.Вставить("НомерВерсии", Число(НомерПоследнейВерсии) + 1);
			СведенияОВерсииОбъекта.ТипВерсииОбъекта = Перечисления.ТипыВерсийОбъекта[СведенияОВерсииОбъекта.ТипВерсииОбъекта];
			
			СоздатьВерсиюОбъекта(Объект, СведенияОВерсииОбъекта, Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Объект.ДополнительныеСвойства.Удалить("СведенияОВерсииОбъекта");
	
КонецПроцедуры

// Заполняет список текущих дел пользователя.
//
// Параметры:
//  ТекущиеДела - ТаблицаЗначений - таблица значений с колонками:
//    * Идентификатор - Строка - внутренний идентификатор дела, используемый механизмом "Текущие дела".
//    * ЕстьДела      - Булево - если Истина, дело выводится в списке текущих дел пользователя.
//    * Важное        - Булево - если Истина, дело будет выделено красным цветом.
//    * Представление - Строка - представление дела, выводимое пользователю.
//    * Количество    - Число  - количественный показатель дела, выводится в строке заголовка дела.
//    * Форма         - Строка - полный путь к форме, которую необходимо открыть при нажатии на гиперссылку
//                               дела на панели "Текущие дела".
//    * ПараметрыФормы- Структура - параметры, с которыми нужно открывать форму показателя.
//    * Владелец      - Строка, объект метаданных - строковый идентификатор дела, которое будет владельцем для текущего
//                      или объект метаданных подсистема.
//    * Подсказка     - Строка - текст подсказки.
//
Процедура ПриЗаполненииСпискаТекущихДел(ТекущиеДела) Экспорт
	
	МодульТекущиеДелаСервер = ОбщегоНазначения.ОбщийМодуль("ТекущиеДелаСервер");
	Если Не ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НастройкиВерсионированияОбъектов)
		Или МодульТекущиеДелаСервер.ДелоОтключено("УстаревшиеВерсииОбъектов") Тогда
		Возврат;
	КонецЕсли;
	
	// Процедура вызывается только при наличии подсистемы "Текущие дела", поэтому здесь
	// не делается проверка существования подсистемы.
	Разделы = МодульТекущиеДелаСервер.РазделыДляОбъекта(Метаданные.РегистрыСведений.НастройкиВерсионированияОбъектов.ПолноеИмя());
	
	ИнформацияОбУстаревшихВерсиях = ИнформацияОбУстаревшихВерсиях();
	РазмерУстаревшихДанных = РазмерДанныхСтрокой(ИнформацияОбУстаревшихВерсиях.РазмерДанных);
	Подсказка = НСтр("ru = 'Устаревших версий: %1 (%2)'");
	
	Для Каждого Раздел Из Разделы Цикл
		
		ИдентификаторУстаревшиеОбъекты = "УстаревшиеВерсииОбъектов" + СтрЗаменить(Раздел.ПолноеИмя(), ".", "");
		// Добавление дела.
		Дело = ТекущиеДела.Добавить();
		Дело.Идентификатор = ИдентификаторУстаревшиеОбъекты;
		// Выводим дело, если размер устаревших данных больше 1 Гб.
		Дело.ЕстьДела      = ИнформацияОбУстаревшихВерсиях.РазмерДанных > (1024 * 1024 * 1024);
		Дело.Представление = НСтр("ru = 'Устаревшие версии объектов'");
		Дело.Форма         = "РегистрСведений.НастройкиВерсионированияОбъектов.Форма.ВерсионированиеОбъектов";
		Дело.Подсказка     = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Подсказка, ИнформацияОбУстаревшихВерсиях.КоличествоВерсий, РазмерУстаревшихДанных);
		Дело.Владелец      = Раздел;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет соответствие имен методов их псевдонимам для вызова из очереди заданий.
//
// Параметры:
//  СоответствиеИменПсевдонимам - Соответствие
//   Ключ - Псевдоним метода, например ОчиститьОбластьДанных.
//   Значение - Имя метода для вызова, например РаботаВМоделиСервиса.ОчиститьОбластьДанных.
//    В качестве значения можно указать Неопределено, в этом случае считается что имя 
//    совпадает с псевдонимом.
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить("ВерсионированиеОбъектов.ОчиститьУстаревшиеВерсииОбъектов");
	
КонецПроцедуры

// Создает и записывает версию объекта в информационную базу.
//
Процедура СоздатьВерсиюОбъекта(Объект, СведенияОВерсииОбъекта, ЗаписьОбычнойВерсии = Истина)
	
	ПроверитьНаличиеПравНаИзменениеОбъекта(Объект.Метаданные());
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗаписьОбычнойВерсии Тогда
		ИзмененаПроведенность = Ложь;
		Если СведенияОВерсииОбъекта.Свойство("ИзмененаПроведенность") Тогда
			ИзмененаПроведенность = СведенияОВерсииОбъекта.ИзмененаПроведенность;
		КонецЕсли;
		
		// Запись данных предыдущей версии.
		Если Не Объект.ЭтоНовый() И (ИзмененаПроведенность И СведенияОВерсииОбъекта.НомерВерсии > 1 Или ВерсияОтличаетсяОтРанееЗаписанной(Объект)) Тогда
			// Если версионирование включено после создания объекта, создаем предыдущую запись о версии.
			Если СведенияОВерсииОбъекта.НомерВерсии = 1 Тогда
				Если ОбъектВерсионируется(Объект.Ссылка, Ложь) Тогда
					ПараметрыВерсии = Новый Структура;
					ПараметрыВерсии.Вставить("НомерВерсии", 1);
					ПараметрыВерсии.Вставить("Комментарий", НСтр("ru = 'Версия создана по уже имеющемуся объекту'"));
					СоздатьВерсиюОбъекта(Объект.Ссылка.ПолучитьОбъект(), ПараметрыВерсии);
					СведенияОВерсииОбъекта.НомерВерсии = 2;
				КонецЕсли;
			КонецЕсли;
			
			// Сохраняем предыдущую версию объекта.
			МенеджерЗаписи = РегистрыСведений.ВерсииОбъектов.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Объект = Объект.Ссылка;
			МенеджерЗаписи.НомерВерсии = СведенияОВерсииОбъекта.НомерВерсии - 1;
			МенеджерЗаписи.Прочитать();
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.ВерсияОбъекта = Новый ХранилищеЗначения(ДанныеДляХранения(Объект.Ссылка), Новый СжатиеДанных(9));
				МенеджерЗаписи.Записать();
			КонецЕсли;
		КонецЕсли;
		
		СсылкаНаОбъект = Объект.Ссылка;;
		Если СсылкаНаОбъект.Пустая() Тогда
			СсылкаНаОбъект = Объект.ПолучитьСсылкуНового();
			Если СсылкаНаОбъект.Пустая() Тогда
				СсылкаНаОбъект = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка).ПолучитьСсылку();
				Объект.УстановитьСсылкуНового(СсылкаНаОбъект);
			КонецЕсли;
		КонецЕсли;
		
		// Запись текущей версии без данных.
		МенеджерЗаписи = РегистрыСведений.ВерсииОбъектов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = СсылкаНаОбъект;
		МенеджерЗаписи.НомерВерсии = СведенияОВерсииОбъекта.НомерВерсии;
		МенеджерЗаписи.ДатаВерсии = ТекущаяДатаСеанса();
		
		АвторВерсии = Неопределено;
		Если Не Объект.ДополнительныеСвойства.Свойство("АвторВерсии", АвторВерсии) Тогда
			АвторВерсии = Пользователи.АвторизованныйПользователь();
		КонецЕсли;
		МенеджерЗаписи.АвторВерсии = АвторВерсии;
		
		МенеджерЗаписи.ТипВерсииОбъекта = Перечисления.ТипыВерсийОбъекта.ИзмененоПользователем;
		СведенияОВерсииОбъекта.Свойство("Комментарий", МенеджерЗаписи.Комментарий);
		
		Если Не Объект.ЭтоНовый() Тогда
			// Перед тем как посчитать контрольную сумму, необходимо установить проведенность в состояние,
			// которое ожидается после записи документа.
			Если ИзмененаПроведенность Тогда
				Объект.Проведен = Не Объект.Проведен;
			КонецЕсли;
			
			МенеджерЗаписи.КонтрольнаяСумма = КонтрольнаяСумма(СериализоватьОбъект(Объект));
			
			// Возвращаем проведенность в исходное состояние, чтобы не поломать другие механизмы, зависящие от этого реквизита.
			Если ИзмененаПроведенность Тогда
				Объект.Проведен = Не Объект.Проведен;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ДвоичныеДанные = СериализоватьОбъект(Объект);
		ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		
		МенеджерЗаписи = РегистрыСведений.ВерсииОбъектов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ДатаВерсии = ТекущаяДатаСеанса();
		МенеджерЗаписи.ВерсияОбъекта = ХранилищеДанных;
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СведенияОВерсииОбъекта);
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

// Записывает версию объекта, полученного при обмене данными, в информационную базу.
//
// Параметры:
//	Объект - для создания версии.
//	СведенияОВерсииОбъекта - Структура - содержит информацию о версии объекта.
//	СсылкаСуществует - Булево - Признак наличия объекта по ссылке в информационной базе.
//
Процедура СоздатьВерсиюОбъектаПоОбменуДанными(Объект, СведенияОВерсииОбъекта, СсылкаСуществует = Неопределено) Экспорт
	
	Ссылка = Объект.Ссылка;
	
	Если Не ЗначениеЗаполнено(СсылкаСуществует) Тогда
		СсылкаСуществует = ОбщегоНазначения.СсылкаСуществует(Ссылка);
	КонецЕсли;
		
	Если СсылкаСуществует Тогда
		
		НомерПоследнейВерсии = НомерПоследнейВерсии(Ссылка);
		
	Иначе
		
		Ссылка = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Ссылка).ПолучитьСсылку(Объект.ПолучитьСсылкуНового().УникальныйИдентификатор());
		НомерПоследнейВерсии = 0;
		
	КонецЕсли;
	
	СведенияОВерсииОбъекта.Вставить("Объект", Ссылка);
	СведенияОВерсииОбъекта.Вставить("НомерВерсии", Число(НомерПоследнейВерсии) + 1);
	СведенияОВерсииОбъекта.ТипВерсииОбъекта = Перечисления.ТипыВерсийОбъекта[СведенияОВерсииОбъекта.ТипВерсииОбъекта];
	
	Если Не ЗначениеЗаполнено(СведенияОВерсииОбъекта.АвторВерсии) Тогда
		СведенияОВерсииОбъекта.АвторВерсии = Пользователи.АвторизованныйПользователь();
	КонецЕсли;
	
	СоздатьВерсиюОбъекта(Объект, СведенияОВерсииОбъекта, Ложь);
	
КонецПроцедуры

// Устанавливает признак игнорирования версии объекта.
//
// Параметры:
//	Ссылка - Ссылка на игнорируемый объект.
//	НомерВерсии - Число - Номер версии игнорируемого объекта.
//	Игнорировать - Булево Признак игнорирования версии.
//
Процедура ИгнорироватьВерсиюОбъекта(Ссылка, НомерВерсии, Игнорировать) Экспорт
	
	ПроверитьНаличиеПравНаИзменениеОбъекта(Ссылка.Метаданные());
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(Ссылка);
	НаборЗаписей.Отбор.НомерВерсии.Установить(НомерВерсии);
	НаборЗаписей.Прочитать();
	
	Запись = НаборЗаписей[0];
	
	Запись.ВерсияПроигнорирована = Игнорировать;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает количество коллизий или непринятых объектов.
//
// Параметры:
//	УзлыОбмена - ПланОбменаСсылка, Массив, СписокЗначений, Неопределено - отбор для получения количества коллизий.
//	ЭтоКоличествоКоллизий - Булево - Если Истина, то возвращает количество коллизий, иначе количество непринятых.
//	ПоказыватьПроигнорированные - Булево - Признак необходимости учета проигнорированных.
//	УзелИнформационнойБазы - ПланОбменаСсылка - Количество по конкретному узлу.
//	Период - Стандартный период - Количество за дату.
//	СтрокаПоиска - Строка - Количество объектов, содержащих в комментарии СтрокаПоиска.
//
Функция КоличествоКоллизийИлиНепринятых(УзлыОбмена, ЭтоКоличествоКоллизий,
	ПоказыватьПроигнорированные, Период, СтрокаПоиска) Экспорт
	
	Количество = 0;
	
	Если Не ЕстьПравоНаЧтениеВерсийОбъектов() Тогда
		Возврат Количество;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(ВерсииОбъектов.Объект) КАК Количество
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.ВерсияПроигнорирована <> &ОтборПоПропущенным
	|	И (ВерсииОбъектов.ТипВерсииОбъекта В (&ТипыВерсий))
	|	[ОтборПоУзлу]
	|	[ОтборПоПериоду]
	|	[ОтборПоПричине]";
	
	Запрос = Новый Запрос;
	
	ОтборПоПропущенным = ?(ПоказыватьПроигнорированные, Неопределено, Истина);
	Запрос.УстановитьПараметр("ОтборПоПропущенным", ОтборПоПропущенным);
	
	Если УзлыОбмена = Неопределено Тогда
		СтрокаОтбора = "";
	ИначеЕсли ПланыОбмена.ТипВсеСсылки().СодержитТип(ТипЗнч(УзлыОбмена)) Тогда
		СтрокаОтбора = "И ВерсииОбъектов.АвторВерсии = &УзлыОбмена";
		Запрос.УстановитьПараметр("УзлыОбмена", УзлыОбмена);
	Иначе
		СтрокаОтбора = "И ВерсииОбъектов.АвторВерсии В (&УзлыОбмена)";
		Запрос.УстановитьПараметр("УзлыОбмена", УзлыОбмена);
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоУзлу]", СтрокаОтбора);
	
	Если ЗначениеЗаполнено(Период) Тогда
		
		СтрокаОтбора = "И (ВерсииОбъектов.ДатаВерсии >= &ДатаНачала
		| И ВерсииОбъектов.ДатаВерсии <= &ДатаОкончания)";
		Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
		Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
		
	Иначе
		
		СтрокаОтбора = "";
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоПериоду]", СтрокаОтбора);
	
	ТипыВерсий = Новый СписокЗначений;
	Если ЗначениеЗаполнено(ЭтоКоличествоКоллизий) Тогда
		
		Если ЭтоКоличествоКоллизий Тогда
			
			ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.ПринятыеДанныеПоКоллизии);
			ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.НепринятыеДанныеПоКоллизии);
			
			СтрокаОтбора = "";
			
		Иначе
			
			ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.НепринятыйПоДатеЗапретаОбъектСуществуетВБазе);
			ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.НепринятыйПоДатеЗапретаОбъектНеСуществуетВБазе);
			
			Если ЗначениеЗаполнено(СтрокаПоиска) Тогда
				
				СтрокаОтбора = "И ВерсииОбъектов.Комментарий ПОДОБНО &Комментарий";
				Запрос.УстановитьПараметр("Комментарий", "%" + СтрокаПоиска + "%");
				
			Иначе
				
				СтрокаОтбора = "";
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе // Отбор по комментарию не поддерживается.
		
		ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.ПринятыеДанныеПоКоллизии);
		ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.НепринятыеДанныеПоКоллизии);
		ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.НепринятыйПоДатеЗапретаОбъектСуществуетВБазе);
		ТипыВерсий.Добавить(Перечисления.ТипыВерсийОбъекта.НепринятыйПоДатеЗапретаОбъектНеСуществуетВБазе);
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ОтборПоПричине]", СтрокаОтбора);
	Запрос.УстановитьПараметр("ТипыВерсий", ТипыВерсий);
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Количество = Выборка.Количество;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

Функция ОбъектИзДвоичныхДанных(ДвоичныеДанные)
	
	ЧтениеXML = Новый ЧтениеFastInfoSet;
	ЧтениеXML.УстановитьДвоичныеДанные(ДвоичныеДанные);
	Если ЧтениеXML.Прочитать() Тогда
		Если ВозможностьЧтенияXML(ЧтениеXML) Тогда
			Объект = ПрочитатьXML(ЧтениеXML);
			ЧтениеXML.Закрыть();
			Возврат Объект;
		Иначе
			ЧтениеXML.Закрыть();
			ВызватьИсключение НСтр("ru = 'Ошибка при восстановлении объекта'");
		КонецЕсли;
	Иначе
		ЧтениеXML.Закрыть();
		ВызватьИсключение НСтр("ru = 'Ошибка чтения данных'");
	КонецЕсли;

КонецФункции

Функция ЗаписьОВерсииОбъекта(СсылкаНаОбъект, НомерВерсии)
	
	НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(СсылкаНаОбъект);
	НаборЗаписей.Отбор.НомерВерсии.Установить(НомерВерсии);
	НаборЗаписей.Прочитать();
	
	Возврат НаборЗаписей;
	
КонецФункции

Процедура ЗаписатьВерсиюПоверхПредыдущей(Ссылка, Объект, НомерВерсии, СведенияОВерсииОбъекта)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = ЗаписьОВерсииОбъекта(Ссылка, НомерВерсии);
	
	Если НаборЗаписей.Количество() = 0 Тогда
		
		ДанныеВерсии = Новый Структура;
		ДанныеВерсии.Вставить("Объект", Ссылка);
		ДанныеВерсии.Вставить("НомерВерсии", Число(НомерВерсии) + 1);
		ДанныеВерсии.Вставить("АвторВерсии", СведенияОВерсииОбъекта.АвторВерсии);
		ДанныеВерсии.Вставить("Комментарий", НСтр("ru = 'Версия создана при синхронизации данных.'"));
		ДанныеВерсии.Вставить("ТипВерсииОбъекта", Перечисления.ТипыВерсийОбъекта[СведенияОВерсииОбъекта.ТипВерсииОбъекта]);
		
		СоздатьВерсиюОбъекта(Объект, ДанныеВерсии, Ложь);
		
	Иначе
		
		ЗаписьОВерсии = НаборЗаписей[0];
		
		ЗаписьXML = Новый ЗаписьFastInfoset;
		ЗаписьXML.УстановитьДвоичныеДанные();
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписатьXML(ЗаписьXML, Объект, НазначениеТипаXML.Явное);
		ДвоичныеДанные = ЗаписьXML.Закрыть();
		ХранилищеДанных = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9));
		
		ЗаписьОВерсии.ДатаВерсии	= ТекущаяДатаСеанса();
		ЗаписьОВерсии.ВерсияОбъекта = ХранилищеДанных;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеПравНаИзменениеОбъекта(ОбъектМетаданных)
	
	Если Не ПривилегированныйРежим() И Не ПравоДоступа("Изменение", ОбъектМетаданных)Тогда
		ТекстСообщения = Нстр("ru = 'Недостаточно прав на изменение ""%1"".'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОбъектМетаданных.Представление());
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПеренестиНастройкиВерсионирования() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УдалитьНастройкиВерсионированияОбъектов.ТипОбъекта КАК ИмяОбъекта,
	|	УдалитьНастройкиВерсионированияОбъектов.Вариант,
	|	УдалитьНастройкиВерсионированияОбъектов.СрокХраненияВерсий
	|ИЗ
	|	РегистрСведений.УдалитьНастройкиВерсионированияОбъектов КАК УдалитьНастройкиВерсионированияОбъектов";

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	НастройкиВерсионирования = РезультатЗапроса.Выгрузить();
	
	НаборЗаписей = РегистрыСведений.НастройкиВерсионированияОбъектов.СоздатьНаборЗаписей();
	Для Каждого НастройкаВерсионирования Из НастройкиВерсионирования Цикл
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(НастройкаВерсионирования.ИмяОбъекта);
		Если ОбъектМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ТипОбъекта = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, НастройкаВерсионирования);
		Запись.ТипОбъекта = ТипОбъекта;
	КонецЦикла;
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
	НаборЗаписей = РегистрыСведений.УдалитьНастройкиВерсионированияОбъектов.СоздатьНаборЗаписей();
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
	
КонецПроцедуры

// Проверяет наличие у пользователя прав на чтение информации о версиях.
//
Функция ЕстьПравоНаЧтениеВерсийОбъектов() Экспорт
	Возврат Пользователи.РолиДоступны("ЧтениеВерсийОбъектов, ЧтениеИнформацииОВерсияхОбъектов");
КонецФункции

Функция ЕстьПравоЧтенияИнформацииОВерсияхОбъектов() Экспорт
	Возврат Пользователи.РолиДоступны("ЧтениеИнформацииОВерсияхОбъектов");
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов в эту подсистему.

// Обработчик перехода на версию объекта.
//
// Параметры:
//	ОбъектСсылка - Ссылка - Ссылка на объект, для которого имеется версия.
//	НомерВерсииДляПерехода - Число - Номер версии, на которую необходимо выполнить переход.
//	НомерИгнорируемойВерсии - Число - Номер версии, на которую необходимо проигнорировать.
//	ПропуститьПроверкуЗапретаИзменения - Булево - Признак пропуска проверки даты запрета загрузки.
//
Процедура ПриПереходеНаВерсиюОбъекта(СсылкаНаОбъект, Знач НомерВерсии) Экспорт
	
	ПроверитьНаличиеПравНаИзменениеОбъекта(СсылкаНаОбъект.Метаданные());
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = ЗаписьОВерсииОбъекта(СсылкаНаОбъект, НомерВерсии);
	Запись = НаборЗаписей[0];
	
	Если Запись.ТипВерсииОбъекта = Перечисления.ТипыВерсийОбъекта.ПринятыеДанныеПоКоллизии Тогда
		
		НомерВерсии = НомерВерсии - 1;
		
		Если НомерВерсии <> 0 Тогда
			
			ПредыдущаяЗапись = ЗаписьОВерсииОбъекта(СсылкаНаОбъект, НомерВерсии)[0];
			Объект = ОбъектИзДвоичныхДанных(ПредыдущаяЗапись.ВерсияОбъекта.Получить());
			ДатаВерсии = ПредыдущаяЗапись.ДатаВерсии;
			
		КонецЕсли;
		
	Иначе
		
		Объект = ОбъектИзДвоичныхДанных(Запись.ВерсияОбъекта.Получить());
		ДатаВерсии = Запись.ДатаВерсии;
		
	КонецЕсли;
	
	Объект.ДополнительныеСвойства.Вставить("ВерсионированиеОбъектовКомментарийКВерсии",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выполнен переход к версии №%1 от %2'"), Строка(НомерВерсии), Формат(ДатаВерсии, "ДЛФ=DT")));
		
	Объект.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗапретаИзменения");
	Объект.Записать();
	
	Запись.ВерсияПроигнорирована = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// См. описание процедуры СтандартныеПодсистемыСервер.СобратьСтатистикуКонфигурации.
Процедура СобратьСтатистикуКонфигурации() Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЦентрМониторинга") Тогда
		Возврат;
	КонецЕсли;
	
	МодульЦентрМониторинга = ОбщегоНазначения.ОбщийМодуль("ЦентрМониторинга");
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТИПЗНАЧЕНИЯ(ВерсииОбъектов.Объект) КАК ТипОбъекта,
	|	СУММА(1) КАК Количество
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТИПЗНАЧЕНИЯ(ВерсииОбъектов.Объект)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Выборка.ТипОбъекта);
		МодульЦентрМониторинга.ЗаписатьСтатистикуОбъектаКонфигурации("КоличествоВерсийОбъекта." + ОбъектМетаданных.ПолноеИмя(), Выборка.Количество);
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НастройкиВерсионированияОбъектов.ТипОбъекта.ПолноеИмя КАК ИмяОбъекта
	|ИЗ
	|	РегистрСведений.НастройкиВерсионированияОбъектов КАК НастройкиВерсионированияОбъектов
	|ГДЕ
	|	НастройкиВерсионированияОбъектов.Использовать = ИСТИНА";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МодульЦентрМониторинга.ЗаписатьСтатистикуОбъектаКонфигурации("ВключеноВерсионированиеОбъекта." + Выборка.ИмяОбъекта, Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает номер последней записанной версии объекта.
//
// Параметры:
//  Ссылка - ЛюбаяСсылка - ссылка на объект информационной базы.
//
// Возвращаемое значение:
//  Число - номер версии объекта.
//
Функция НомерПоследнейВерсии(Ссылка) Экспорт
	
	Если Ссылка.Пустая() Тогда
		Возврат 0;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВерсииОбъектов.НомерВерсии), 0) КАК НомерВерсии
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.Объект = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.НомерВерсии;
	
КонецФункции

// Возвращает полные имена объектов метаданных, для которых подключен механизм версионирования.
//
// Возвращаемое значение:
//  Массив строк - имена объектов метаданных.
//
Функция ПолучитьВерсионируемыеОбъекты()
	
	Результат = Новый Массив;
	
	Для Каждого Тип Из Метаданные.ОбщиеКоманды.ИсторияИзменений.ТипПараметраКоманды.Типы() Цикл
		Результат.Добавить(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Тип));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает вариант версионирования для указанного объекта метаданных.
//
// Параметры:
//  ТипОбъекта - СправочникСсылка.ИдентификаторОбъектаМетаданных - ИОМ.
//
// Возвращаемое значение:
//  Перечисление.ВариантыВерсионированияОбъектов.
//
Функция ВариантВерсионированияОбъекта(ТипОбъекта)
	
	Возврат ПолучитьФункциональнуюОпцию("ВариантыВерсионированияОбъектов",
		Новый Структура("ТипВерсионируемогоОбъекта", ТипОбъекта));
		
КонецФункции	

// Получает объект по его сериализованному XML представлению.
//
// Параметры:
//  АдресВоВременномХранилище - Строка - адрес двоичных данных во временном хранилище.
//  ТекстСообщенияОбОшибке    - Строка - текст ошибки (возвращаемый параметр), если восстановить объект не удалось.
//
// Возвращаемое значение - Объект или Неопределено.
//
Функция ВосстановитьОбъектПоXML(Знач АдресВоВременномХранилище = "", ТекстСообщенияОбОшибке = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
	
	ЧтениеFastInfoSet = Новый ЧтениеFastInfoSet;
	ЧтениеFastInfoSet.УстановитьДвоичныеДанные(ДвоичныеДанные);
	
	Попытка
		Объект = ПрочитатьXML(ЧтениеFastInfoSet);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Версионирование'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ТекстСообщенияОбОшибке = НСтр("ru = 'Не удалось перейти на выбранную версию.
											|Возможная причина: версия объекта была записана в другой версии программы.
											|Техническая информация об ошибке: %1'");
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщенияОбОшибке, КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Объект;
	
КонецФункции

// Возвращает структуру, содержащую версию объекта и дополнительную информацию.
//
// Параметры:
//  Ссылка      - Ссылка - ссылка версионируемый объект;
//  НомерВерсии - Число  - номер версии объекта.
//
// Возвращаемое значение - Структура:
//                          ВерсияОбъекта - ДвоичныеДанные - сохраненная версия объекта информационной базы;
//                          АвторВерсии   - Справочник.Пользователи, Справочник.ВнешниеПользователи - 
//                                          пользователь, записавший версию объекта.
//                          ДатаВерсии    - Дата - дата записи версии объекта.
// 
// Примечание:
//  Функция может вызвать исключение, если запись не содержит данных.
//  Функцию требуется вызвать в привилегированном режиме.
//
Функция СведенияОВерсииОбъекта(Знач Ссылка, Знач НомерВерсии) Экспорт
	СообщениеНеУдалосьПолучитьВерсию = НСтр("ru = 'Не удалось получить предыдущую версию объекта.'");
	Если Не Пользователи.РолиДоступны("ЧтениеВерсийОбъектов") Тогда
		ВызватьИсключение СообщениеНеУдалосьПолучитьВерсию;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВерсииОбъектов.АвторВерсии КАК АвторВерсии,
	|	ВерсииОбъектов.ДатаВерсии КАК ДатаВерсии,
	|	ВерсииОбъектов.Комментарий КАК Комментарий,
	|	ВерсииОбъектов.ВерсияОбъекта,
	|	ВерсииОбъектов.КонтрольнаяСумма
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.Объект = &Ссылка
	|	И ВерсииОбъектов.НомерВерсии = &НомерВерсии";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НомерВерсии", Число(НомерВерсии));
	
	Результат = Новый Структура("ВерсияОбъекта, ТабличныеДокументы, АвторВерсии, ДатаВерсии, Комментарий");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
		Результат.ВерсияОбъекта = Результат.ВерсияОбъекта.Получить();
		Если Результат.ВерсияОбъекта = Неопределено Тогда
			Результат.ВерсияОбъекта = ДанныеВерсииОбъекта(Ссылка, НомерВерсии, Выборка.КонтрольнаяСумма);
		КонецЕсли;
		
		Если ТипЗнч(Результат.ВерсияОбъекта) = Тип("Структура") Тогда
			Результат.ТабличныеДокументы = Результат.ВерсияОбъекта.ТабличныеДокументы;
			Результат.ВерсияОбъекта = Результат.ВерсияОбъекта.Объект;
		КонецЕсли;
	КонецЕсли;
	
	Если Результат.ВерсияОбъекта = Неопределено Тогда
		ВызватьИсключение НСтр("ru = 'Выбранная версия объекта отсутствует в программе.'");
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Проверяет настройки версионирования по переданному объекту и
// и возвращает вариант версионирования. Если по объекту не настроено
// версионирование, то он версионируется в соответствии с правилами
// версионирования "по умолчанию".
//
Функция ОбъектВерсионируется(Знач Источник, НомерПоследнейВерсии, РежимЗаписиПроведение = Ложь) Экспорт
	
	// Проверяем, что подсистема версионирования включена.
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьВерсионированиеОбъектов") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВариантВерсионирования = ВариантВерсионированияОбъекта(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Источник.Метаданные()));
	Если ВариантВерсионирования = Ложь Тогда
		ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.НеВерсионировать;
	КонецЕсли;
	
	НомерПоследнейВерсии = НомерПоследнейВерсии(Источник.Ссылка);
	
	Возврат НомерПоследнейВерсии > 0 
		Или ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриЗаписи
		Или ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриПроведении И (РежимЗаписиПроведение Или Источник.Проведен)
		Или ВариантВерсионирования = Перечисления.ВариантыВерсионированияОбъектов.ВерсионироватьПриСтарте И Источник.Стартован;
	
КонецФункции

// Добавляет в дополнительные свойства сведения для записи версии объекта,
// полученного при обмене данными.
//
Процедура ДобавлениеСведенийОВерсииОбъектаПриОбменеДанными(Объект, АвторВерсии)
	
	Если ТипЗнч(Объект) <> Тип("УдалениеОбъекта") И ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(Объект.Метаданные()) Тогда
		
		СведенияОВерсииОбъекта = Новый Структура;
		СведенияОВерсииОбъекта.Вставить("АвторВерсии", АвторВерсии);
		СведенияОВерсииОбъекта.Вставить("ТипВерсииОбъекта", "ИзмененоПользователем");
		СведенияОВерсииОбъекта.Вставить("Комментарий", НСтр("ru = 'Версия получена при синхронизации данных.'"));
		СведенияОВерсииОбъекта.Вставить("ОтложеннаяОбработка", Ложь);
		Объект.ДополнительныеСвойства.Вставить("СведенияОВерсииОбъекта", Новый ФиксированнаяСтруктура(СведенияОВерсииОбъекта));
		
	КонецЕсли;
	
КонецПроцедуры

// Записывает версию объекта в информационную базу.
//
// Параметры:
//  Источник - Объект - записываемый объект ИБ;
//  РежимЗаписи - РежимЗаписиДокумента.
//
//Процедура ЗаписатьВерсиюОбъекта(Источник, РежимЗаписиПроведение = Ложь) Экспорт
Процедура ЗаписатьВерсиюОбъекта(Источник, РежимЗаписи = Неопределено) Экспорт
	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если СтандартныеПодсистемыСервер.ЭтоИдентификаторОбъектаМетаданных(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("СведенияОВерсииОбъекта") Тогда
		Возврат;
	КонецЕсли;
	
	ПриСозданииВерсииОбъекта(Источник, РежимЗаписи);
	
КонецПроцедуры

// Контрольная сумма по алгоритму MD5.
Функция КонтрольнаяСумма(Данные) Экспорт
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(Данные);
	Возврат СтрЗаменить(ХешированиеДанных.ХешСумма, " ", "");
КонецФункции

Функция ДанныеВерсииОбъекта(СсылкаНаОбъект, НомерВерсии, КонтрольнаяСумма)
	
	Если Не ПустаяСтрока(КонтрольнаяСумма) Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВерсииОбъектов.ВерсияОбъекта,
		|	ВерсииОбъектов.НомерВерсии
		|ИЗ
		|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
		|ГДЕ
		|	ВерсииОбъектов.Объект = &Объект
		|	И ВерсииОбъектов.НомерВерсии >= &НомерВерсии
		|	И ВерсииОбъектов.КонтрольнаяСумма = &КонтрольнаяСумма
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВерсииОбъектов.НомерВерсии УБЫВ";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Объект", СсылкаНаОбъект);
		Запрос.УстановитьПараметр("НомерВерсии", НомерВерсии);
		Запрос.УстановитьПараметр("КонтрольнаяСумма", КонтрольнаяСумма);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Результат = Выборка.ВерсияОбъекта.Получить();
			Если Результат = Неопределено И Выборка.НомерВерсии = НомерПоследнейВерсии(СсылкаНаОбъект) Тогда
				Результат = ДанныеДляХранения(СсылкаНаОбъект);
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВерсииОбъектов.КонтрольнаяСумма
		|ИЗ
		|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
		|ГДЕ
		|	ВерсииОбъектов.Объект = &Объект
		|	И ВерсииОбъектов.НомерВерсии >= &НомерВерсии
		|	И ВерсииОбъектов.КонтрольнаяСумма <> """"
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВерсииОбъектов.НомерВерсии";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Объект", СсылкаНаОбъект);
		Запрос.УстановитьПараметр("НомерВерсии", НомерВерсии);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Возврат ДанныеВерсииОбъекта(СсылкаНаОбъект, НомерВерсии, Выборка.КонтрольнаяСумма);
		КонецЕсли;
		
		Возврат ДанныеДляХранения(СсылкаНаОбъект);
	КонецЕсли;
	
КонецФункции

Функция ВерсияОтличаетсяОтРанееЗаписанной(Объект)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВерсииОбъектов.КонтрольнаяСумма
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.Объект = &Объект
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВерсииОбъектов.НомерВерсии УБЫВ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Объект", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Не ПустаяСтрока(Выборка.КонтрольнаяСумма) Тогда
		Возврат Выборка.КонтрольнаяСумма <> КонтрольнаяСумма(СериализоватьОбъект(Объект));
	КонецЕсли;
	
	Возврат Объект.ЭтоНовый() Или КонтрольнаяСумма(СериализоватьОбъект(Объект)) <> КонтрольнаяСумма(СериализоватьОбъект(Объект.Ссылка.ПолучитьОбъект()));
	
КонецФункции

// Только для служебного использования.
Процедура ОчиститьУстаревшиеВерсииОбъектов() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОчисткаУстаревшихВерсийОбъектов);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГраницыУдаленияОбъектов = ГраницыУдаленияОбъектов();
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВерсииОбъектов.Объект,
	|	ВерсииОбъектов.НомерВерсии
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.ЕстьДанныеВерсии
	|	И &ДополнительныеУсловия";
	
	ДополнительныеУсловия = "";
	Для Индекс = 0 По ГраницыУдаленияОбъектов.Количество() - 1 Цикл
		Если Не ПустаяСтрока(ДополнительныеУсловия) Тогда
			ДополнительныеУсловия = ДополнительныеУсловия + "
			|	ИЛИ";
		КонецЕсли;
		ИндексСтрокой = Формат(Индекс, "ЧН=0; ЧГ=0");
		Условие = "";
		Для Каждого Тип Из ГраницыУдаленияОбъектов[Индекс].СписокТипов Цикл
			Если Не ПустаяСтрока(Условие) Тогда
				Условие = Условие + "
				|	ИЛИ";
			КонецЕсли;
			Условие = Условие + "
			|	ВерсииОбъектов.Объект ССЫЛКА " + Тип;
		КонецЦикла;
		Если ПустаяСтрока(Условие) Тогда
			Продолжить;
		КонецЕсли;
		Условие = "(" + Условие + ")";
		ДополнительныеУсловия = ДополнительныеУсловия + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"
			|	%1
			|	И ВерсииОбъектов.ДатаВерсии < &ГраницаУдаления%2",
			Условие,
			ИндексСтрокой);
		Запрос.УстановитьПараметр("СписокТипов" + ИндексСтрокой, ГраницыУдаленияОбъектов[Индекс].СписокТипов);
		Запрос.УстановитьПараметр("ГраницаУдаления" + ИндексСтрокой, ГраницыУдаленияОбъектов[Индекс].ГраницаУдаления);
	КонецЦикла;

	Если ПустаяСтрока(ДополнительныеУсловия) Тогда
		ДополнительныеУсловия = "ЛОЖЬ";
	Иначе
		ДополнительныеУсловия = "(" + ДополнительныеУсловия + ")";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДополнительныеУсловия", ДополнительныеУсловия);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ВерсииОбъектов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Объект = Выборка.Объект;
		МенеджерЗаписи.НомерВерсии = Выборка.НомерВерсии;
		МенеджерЗаписи.Прочитать();
		МенеджерЗаписи.ВерсияОбъекта = Неопределено;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры

Функция ГраницыУдаленияОбъектов() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("СписокТипов", Новый ОписаниеТипов("Массив"));
	Результат.Колонки.Добавить("ГраницаУдаления", Новый ОписаниеТипов("Дата"));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НастройкиВерсионированияОбъектов.ТипОбъекта.ПолноеИмя КАК ТипОбъекта,
	|	НастройкиВерсионированияОбъектов.СрокХраненияВерсий КАК СрокХраненияВерсий
	|ИЗ
	|	РегистрСведений.НастройкиВерсионированияОбъектов КАК НастройкиВерсионированияОбъектов
	|ГДЕ
	|	НЕ НастройкиВерсионированияОбъектов.ТипОбъекта.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	СрокХраненияВерсий
	|ИТОГИ ПО
	|	СрокХраненияВерсий";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ВыборкаСроковХранения = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСроковХранения.Следующий() Цикл
		ВыборкаОбъектов = ВыборкаСроковХранения.Выбрать();
		СписокТипов = Новый Массив;
		Пока ВыборкаОбъектов.Следующий() Цикл
			СписокТипов.Добавить(ВыборкаОбъектов.ТипОбъекта);
		КонецЦикла;
		СоответствиеГраницыТипамОбъектов = Результат.Добавить();
		СоответствиеГраницыТипамОбъектов.ГраницаУдаления = ГраницаУдаления(ВыборкаСроковХранения.СрокХраненияВерсий);
		СоответствиеГраницыТипамОбъектов.СписокТипов = СписокТипов;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ГраницаУдаления(СрокХраненияВерсий)
	Если СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.ЗаПоследнийГод Тогда
		Возврат ДобавитьМесяц(ТекущаяДатаСеанса(), -12);
	ИначеЕсли СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.ЗаПоследниеШестьМесяцев Тогда
		Возврат ДобавитьМесяц(ТекущаяДатаСеанса(), -6);
	ИначеЕсли СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.ЗаПоследниеТриМесяца Тогда
		Возврат ДобавитьМесяц(ТекущаяДатаСеанса(), -3);
	ИначеЕсли СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.ЗаПоследнийМесяц Тогда
		Возврат ДобавитьМесяц(ТекущаяДатаСеанса(), -1);
	ИначеЕсли СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.ЗаПоследнююНеделю Тогда
		Возврат ТекущаяДатаСеанса() - 7*24*60*60;
	Иначе // СрокХраненияВерсий = Перечисления.СрокиХраненияВерсий.Бессрочно
		Возврат '000101010000';
	КонецЕсли;
КонецФункции

// Только для служебного использования.
// Комментарий записывается, если пользователь является либо автором версии, либо администратором.
Процедура ДобавитьКомментарийКВерсии(СсылкаНаОбъект, НомерВерсии, Комментарий) Экспорт
	
	Если Не ЕстьПравоНаЧтениеВерсийОбъектов() Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.ВерсииОбъектов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Объект = СсылкаНаОбъект;
	МенеджерЗаписи.НомерВерсии = НомерВерсии;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		Если МенеджерЗаписи.АвторВерсии = Пользователи.ТекущийПользователь() Или Пользователи.ЭтоПолноправныйПользователь(, , Ложь) Тогда
			МенеджерЗаписи.Комментарий = Комментарий;
			МенеджерЗаписи.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Предоставляет информацию о количестве и объеме устаревших версий объектов.
Функция ИнформацияОбУстаревшихВерсиях() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГраницыУдаленияОбъектов = ГраницыУдаленияОбъектов();
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВерсииОбъектов.РазмерДанных), 0) КАК РазмерДанных,
	|	ЕСТЬNULL(СУММА(1), 0) КАК КоличествоВерсий
	|ИЗ
	|	РегистрСведений.ВерсииОбъектов КАК ВерсииОбъектов
	|ГДЕ
	|	ВерсииОбъектов.ЕстьДанныеВерсии
	|	И &ДополнительныеУсловия";
	
	ДополнительныеУсловия = "";
	Для Индекс = 0 По ГраницыУдаленияОбъектов.Количество() - 1 Цикл
		Если Не ПустаяСтрока(ДополнительныеУсловия) Тогда
			ДополнительныеУсловия = ДополнительныеУсловия + "
			|	ИЛИ";
		КонецЕсли;
		ИндексСтрокой = Формат(Индекс, "ЧН=0; ЧГ=0");
		Условие = "";
		Для Каждого Тип Из ГраницыУдаленияОбъектов[Индекс].СписокТипов Цикл
			Если Не ПустаяСтрока(Условие) Тогда
				Условие = Условие + "
				|	ИЛИ";
			КонецЕсли;
			Условие = Условие + "
			|	ВерсииОбъектов.Объект ССЫЛКА " + Тип;
		КонецЦикла;
		Если ПустаяСтрока(Условие) Тогда
			Продолжить;
		КонецЕсли;
		Условие = "(" + Условие + ")";
		ДополнительныеУсловия = ДополнительныеУсловия + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"
			|	%1
			|	И ВерсииОбъектов.ДатаВерсии < &ГраницаУдаления%2",
			Условие,
			ИндексСтрокой);
		Запрос.УстановитьПараметр("СписокТипов" + ИндексСтрокой, ГраницыУдаленияОбъектов[Индекс].СписокТипов);
		Запрос.УстановитьПараметр("ГраницаУдаления" + ИндексСтрокой, ГраницыУдаленияОбъектов[Индекс].ГраницаУдаления);
	КонецЦикла;

	Если ПустаяСтрока(ДополнительныеУсловия) Тогда
		ДополнительныеУсловия = "ЛОЖЬ";
	Иначе
		ДополнительныеУсловия = "(" + ДополнительныеУсловия + ")";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДополнительныеУсловия", ДополнительныеУсловия);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	КоличествоВерсий = 0;
	РазмерДанных = 0;
	Если Выборка.Следующий() Тогда
		РазмерДанных = Выборка.РазмерДанных;
		КоличествоВерсий = Выборка.КоличествоВерсий;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("КоличествоВерсий", КоличествоВерсий);
	Результат.Вставить("РазмерДанных", РазмерДанных);
	
	Возврат Результат;
	
КонецФункции

// См. ИнформацияОбУстаревшихВерсиях.
Процедура ИнформацияОбУстаревшихВерсияхВФоне(ДополнительныеПараметры, АдресВоВременномХранилище) Экспорт
	Результат = ИнформацияОбУстаревшихВерсиях();
	Результат.Вставить("РазмерДанныхСтрокой", РазмерДанныхСтрокой(Результат.РазмерДанных));
	ПоместитьВоВременноеХранилище(Результат, АдресВоВременномХранилище);
КонецПроцедуры

// Строковое представление объемов данных. Например: "1,23 Гб".
Функция РазмерДанныхСтрокой(Знач РазмерДанных) Экспорт
	
	ЕдиницаИзмерения = НСтр("ru = 'байт'");
	Если 1024 <= РазмерДанных И РазмерДанных < 1024 * 1024 Тогда
		РазмерДанных = РазмерДанных / 1024;
		ЕдиницаИзмерения = НСтр("ru = 'Кб'");
	ИначеЕсли 1024 * 1024 <= РазмерДанных И  РазмерДанных < 1024 * 1024 * 1024 Тогда
		РазмерДанных = РазмерДанных / 1024 / 1024;
		ЕдиницаИзмерения = НСтр("ru = 'Мб'");
	ИначеЕсли 1024 * 1024 * 1024 <= РазмерДанных Тогда
		РазмерДанных = РазмерДанных / 1024 / 1024 / 1024;
		ЕдиницаИзмерения = НСтр("ru = 'Гб'");
	КонецЕсли;
	
	Если РазмерДанных < 10 Тогда
		РазмерДанных = Окр(РазмерДанных, 2);
	ИначеЕсли РазмерДанных < 100 Тогда
		РазмерДанных = Окр(РазмерДанных, 1);
	Иначе
		РазмерДанных = Окр(РазмерДанных, 0);
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 %2'"), РазмерДанных, ЕдиницаИзмерения);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Функции для получения отчета по объекту.

// Возвращает сериализованный объект в виде двоичных данных.
//
// Параметры:
//  Объект - Любой - сериализуемый объект.
//
// Возвращаемое значение:
//  ДвоичныеДанные - сериализованный объект.
Функция СериализоватьОбъект(Объект) Экспорт
	
	ЗаписьXML = Новый ЗаписьFastInfoset;
	ЗаписьXML.УстановитьДвоичныеДанные();
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписатьXML(ЗаписьXML, Объект, НазначениеТипаXML.Явное);
	
	Возврат ЗаписьXML.Закрыть();

КонецФункции

// Процедура считывает данные XML из файла и заполняет структуры данных.
//
// Возвращаемое значение:
// Структура, содержащая два соответствия: ТабличныеЧасти, Реквизиты.
// Структура хранения данных:
// Соответствие ТабличныеЧасти, которое содержит в себе значения табличных частей
// формат: 
//          СоответствиеИмя1 -> ТаблицаЗначений1
//                            |      |     ... |
//                            Поле1  Поле2     ПолеM1
//
//          СоответствиеИмя2 -> ТаблицаЗначений2
//                            |      |     ... |
//                            Поле1  Поле2     ПолеM2
//
//
//          СоответствиеИмяN -> ТаблицаЗначенийN
//                            |      |     ... |
//                            Поле1  Поле2     ПолеM3
//
// Соответствие ЗначенияРеквизитов
//          ИмяРеквизита1 -> Значение1
//          ИмяРеквизита2 -> Значение2
//          ...
//          ИмяРеквизитаN -> ЗначениеN
//
Функция РазборПредставленияОбъектаXML(ДвоичныеДанные, Ссылка) Экспорт
	
	// Содержит имя метаданного измененного объекта.
	Перем ИмяОбъекта;
	
	// Содержит положение маркера в дереве XML.
	// Требуется для идентификации текущего элемента.
	Перем УровеньЧтения;
	
	// Содержат значения реквизитов справочников / документов.
	ЗначенияРеквизитов = Новый ТаблицаЗначений;
	
	ЗначенияРеквизитов.Колонки.Добавить("НаименованиеРеквизита");
	ЗначенияРеквизитов.Колонки.Добавить("ЗначениеРеквизита");
	ЗначенияРеквизитов.Колонки.Добавить("ТипРеквизита");
	ЗначенияРеквизитов.Колонки.Добавить("Тип");
	
	ТабличныеЧасти = Новый Соответствие;
	
	ЧтениеXML = Новый ЧтениеFastInfoSet;
	ЧтениеXML.УстановитьДвоичныеДанные(ДвоичныеДанные);
	
	// Уровень позиции маркера в иерархии XML:
	// 0 - уровень не задан
	// 1 - первый элемент (имя объекта)
	// 2 - описание реквизита или табличной части
	// 3 - описание строки табличной части
	// 4 - описание поля строки табличной части.
	УровеньЧтения = 0;
	
	МетаданныеОбъекта = Ссылка.Метаданные();
	ТабличныеЧастиМТД = МетаданныеОбъекта.ТабличныеЧасти;
	ТипЗначения = "";
	ТипЗначенияПоляТЧ = "";
	
	// Основной цикл разбора по XML.
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			УровеньЧтения = УровеньЧтения + 1;
			Если УровеньЧтения = 1 Тогда // Указатель на первом элементе XML - корень XML.
				ИмяОбъекта = ЧтениеXML.Имя;
			ИначеЕсли УровеньЧтения = 2 Тогда // Указатель на втором уровне - это реквизит или имя табличной части.
				ИмяРеквизита = ЧтениеXML.Имя;
				
				// Любой реквизит "может оказаться" табличной частью, поэтому на всякий случай его запомним.
				ИмяТабличнойЧасти = ИмяРеквизита;
				Если МетаданныеОбъекта.ТабличныеЧасти.Найти(ИмяТабличнойЧасти) <> Неопределено И ТабличныеЧасти[ИмяТабличнойЧасти] = Неопределено Тогда
					ТабличныеЧасти.Вставить(ИмяТабличнойЧасти, Новый ТаблицаЗначений);
				КонецЕсли;
				
				НовоеЗР = ЗначенияРеквизитов.Добавить();
				НовоеЗР.НаименованиеРеквизита = ИмяРеквизита;
				
				Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
					Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
						Если ЧтениеXML.ТипУзла = ТипУзлаXML.Атрибут 
							И ЧтениеXML.Имя = "xsi:type" Тогда
								НовоеЗР.ТипРеквизита = ЧтениеXML.Значение;
								XMLТип = ЧтениеXML.Значение;
								Если СтрНачинаетсяС(XMLТип, "xs:") Тогда
									НовоеЗР.Тип = ИзXMLТипа(Новый ТипДанныхXML(Прав(XMLТип, СтрДлина(XMLТип)-3), "http://www.w3.org/2001/XMLSchema"));
								Иначе
									НовоеЗР.Тип = ИзXMLТипа(Новый ТипДанныхXML(XMLТип, ""));
								КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(НовоеЗР.Тип) Тогда
					ОписаниеРеквизита = МетаданныеОбъекта.Реквизиты.Найти(НовоеЗР.НаименованиеРеквизита);
					Если ОписаниеРеквизита = Неопределено Тогда
						НаименованиеРеквизита = ПолучитьПредставлениеРеквизитаНаЯзыке(НовоеЗР.НаименованиеРеквизита);
						Если ОбщегоНазначения.ЭтоСтандартныйРеквизит(МетаданныеОбъекта.СтандартныеРеквизиты, НаименованиеРеквизита) Тогда
							ОписаниеРеквизита = МетаданныеОбъекта.СтандартныеРеквизиты[НаименованиеРеквизита];
						Иначе
							ОписаниеРеквизита = Метаданные.ОбщиеРеквизиты.Найти(НаименованиеРеквизита);
						КонецЕсли;
					КонецЕсли;
					
					Если ОписаниеРеквизита <> Неопределено
						И ОписаниеРеквизита.Тип.Типы().Количество() = 1 Тогда
						НовоеЗР.Тип = ОписаниеРеквизита.Тип.Типы()[0];
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли (УровеньЧтения = 3) И (ЧтениеXML.Имя = "Row") Тогда // Указатель на поле табличной части.
				Если ТабличныеЧасти[ИмяТабличнойЧасти] = Неопределено Тогда
					ТабличныеЧасти.Вставить(ИмяТабличнойЧасти, Новый ТаблицаЗначений);
				КонецЕсли;
				
				ТабличныеЧасти[ИмяТабличнойЧасти].Добавить();
			ИначеЕсли УровеньЧтения = 4 Тогда // Указатель на поле табличной части.
				ТипЗначенияПоляТЧ = "";
				ИмяПоляТЧ = ЧтениеXML.Имя;
				Таблица   = ТабличныеЧасти[ИмяТабличнойЧасти];
				Если Таблица.Колонки.Найти(ИмяПоляТЧ)= Неопределено Тогда
					Таблица.Колонки.Добавить(ИмяПоляТЧ);
				КонецЕсли;
				
				Если ЧтениеXML.КоличествоАтрибутов() > 0 Тогда
					Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
						Если ЧтениеXML.ТипУзла = ТипУзлаXML.Атрибут 
							И ЧтениеXML.Имя = "xsi:type" Тогда
								XMLТип = ЧтениеXML.Значение;
								Если СтрНачинаетсяС(XMLТип, "xs:") Тогда
									ТипЗначенияПоляТЧ = ИзXMLТипа(Новый ТипДанныхXML(Прав(XMLТип, СтрДлина(XMLТип)-3), "http://www.w3.org/2001/XMLSchema"));
								Иначе
									ТипЗначенияПоляТЧ = ИзXMLТипа(Новый ТипДанныхXML(XMLТип, ""));
								КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			УровеньЧтения = УровеньЧтения - 1;
			ТипЗначения = "";
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			Если (УровеньЧтения = 2) Тогда // значение реквизита
				Попытка
					НовоеЗР.ЗначениеРеквизита = ?(ЗначениеЗаполнено(НовоеЗР.Тип), XMLЗначение(НовоеЗР.Тип, ЧтениеXML.Значение), ЧтениеXML.Значение);
				Исключение
					НовоеЗР.ЗначениеРеквизита = ЧтениеXML.Значение;
				КонецПопытки;
			ИначеЕсли (УровеньЧтения = 4) Тогда // значение реквизита
				ПоследняяСтрока = ТабличныеЧасти[ИмяТабличнойЧасти].Получить(ТабличныеЧасти[ИмяТабличнойЧасти].Количество()-1);
				
				Если ТипЗначенияПоляТЧ = "" Тогда
					ОписаниеРТЧ = Неопределено;
					Если ТабличныеЧастиМТД.Найти(ИмяТабличнойЧасти) <> Неопределено Тогда
						ОписаниеРТЧ = ТабличныеЧастиМТД[ИмяТабличнойЧасти].Реквизиты.Найти(ИмяПоляТЧ);
						Если ОписаниеРТЧ <> Неопределено
							И ОписаниеРТЧ.Тип.Типы().Количество() = 1 Тогда
								ТипЗначенияПоляТЧ = ОписаниеРТЧ.Тип.Типы()[0];
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ПоследняяСтрока[ИмяПоляТЧ] = ?(ЗначениеЗаполнено(ТипЗначенияПоляТЧ), XMLЗначение(ТипЗначенияПоляТЧ, ЧтениеXML.Значение), ЧтениеXML.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Й-й этап: из списка реквизитов исключаем табличные части.
	Для Каждого Элемент Из ТабличныеЧасти Цикл
		ЗначенияРеквизитов.Удалить(ЗначенияРеквизитов.Найти(Элемент.Ключ));
	КонецЦикла;
	// ТабличныеЧастиМТД
	Для Каждого ЭлементСоответствия Из ТабличныеЧасти Цикл
		Таблица = ЭлементСоответствия.Значение;
		Если Таблица.Колонки.Количество() = 0 Тогда
			ТаблицаМТД = ТабличныеЧастиМТД.Найти(ЭлементСоответствия.Ключ);
			Если ТаблицаМТД <> Неопределено Тогда
				Для Каждого ОписаниеКолонки Из ТаблицаМТД.Реквизиты Цикл
					Если Таблица.Колонки.Найти(ОписаниеКолонки.Имя)= Неопределено Тогда
						Таблица.Колонки.Добавить(ОписаниеКолонки.Имя);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("Реквизиты", ЗначенияРеквизитов);
	Результат.Вставить("ТабличныеЧасти", ТабличныеЧасти);
	
	Возврат Результат;
	
КонецФункции

// Получает представление наименования системного реквизита.
//
Функция ПолучитьПредставлениеРеквизитаНаЯзыке(Знач ИмяРеквизита) Экспорт
	
	Если      ИмяРеквизита = "Number" Тогда
		Возврат НСтр("ru = 'Номер'; en='Number'");
	ИначеЕсли ИмяРеквизита = "Name" Тогда
		Возврат НСтр("ru = 'Наименование'; en='Name'");
	ИначеЕсли ИмяРеквизита = "Code" Тогда
		Возврат НСтр("ru = 'Код'; en='Code'");
	ИначеЕсли ИмяРеквизита = "IsFolder" Тогда
		Возврат НСтр("ru = 'ЭтоГруппа'; en='Is folder'");
	ИначеЕсли ИмяРеквизита = "Description" Тогда
		Возврат НСтр("ru = 'Наименование'; en='Description'");
	ИначеЕсли ИмяРеквизита = "Date" Тогда
		Возврат НСтр("ru = 'Дата'; en='Date'");
	ИначеЕсли ИмяРеквизита = "Posted" Тогда
		Возврат НСтр("ru = 'Проведен'; en='Posted'");
	ИначеЕсли ИмяРеквизита = "DeletionMark" Тогда
		Возврат НСтр("ru = 'ПометкаУдаления'; en='Deletion mark'");
	ИначеЕсли ИмяРеквизита = "Ref" Тогда
		Возврат НСтр("ru = 'Ссылка'; en='Ref'");
	ИначеЕсли ИмяРеквизита = "Parent" Тогда
		Возврат НСтр("ru = 'Родитель'; en='Parent'");
	ИначеЕсли ИмяРеквизита = "Owner" Тогда
		Возврат НСтр("ru = 'Владелец'; en='Owner'");
	Иначе
		Возврат ИмяРеквизита;
	КонецЕсли;
	
КонецФункции

Процедура СформироватьОтчетПоВерсииОбъекта(ТабличныйДокумент, ОписаниеОбъекта, СсылкаНаОбъект)
	
	Если СсылкаНаОбъект.Метаданные().Макеты.Найти("МакетОбъекта") <> Неопределено Тогда
		Макет = ОбщегоНазначения.МенеджерОбъектаПоСсылке(СсылкаНаОбъект).ПолучитьМакет("МакетОбъекта");
	Иначе
		Макет = Неопределено;
	КонецЕсли;
	
	Если Макет = Неопределено Тогда
		Секция = ТабличныйДокумент.ПолучитьОбласть("R2");
		ВывестиТекстВОтчет(ТабличныйДокумент, Секция, "R2C2", СсылкаНаОбъект.Метаданные().Синоним, 16, Истина);
		
		ТабличныйДокумент.Область("C2").ШиринаКолонки = 30;
		Если ОписаниеОбъекта.НомерВерсии <> 0 Тогда
			ВывестиШапкуПоВерсии(ТабличныйДокумент, ОписаниеОбъекта.Описание, 4, 3);
			ВывестиШапкуПоВерсии(ТабличныйДокумент, ОписаниеОбъекта.Комментарий, 5, 3);
		КонецЕсли;
		
		ЧислоВыведенныхСтрок = ВывестиРеквизитыПоРазобранномуОбъекту(ТабличныйДокумент, ОписаниеОбъекта, СсылкаНаОбъект);
		ЧислоВыведенныхСтрок = ВывестиТабличныеЧастиПоРазобранномуОбъекту(ТабличныйДокумент, ОписаниеОбъекта, ЧислоВыведенныхСтрок + 7, СсылкаНаОбъект);
		ВывестиТабличныеДокументыПоРазобранномуОбъекту(ТабличныйДокумент, ОписаниеОбъекта);
	Иначе
		СформироватьПоСтандартномуМакету(ТабличныйДокумент, Макет, ОписаниеОбъекта, ОписаниеОбъекта.Описание, СсылкаНаОбъект);
	КонецЕсли;
	
КонецПроцедуры

// Формирует отчет по объекту, используя стандартный макет.
//
// Параметры:
// ТЧОтчета - ТабличныйДокумент - табличный документ, в который будет выводится отчет.
// ВерсияОбъекта - СправочникОбъект,ДокументОбъект - объект, данные которого необходимо отобразить в отчете.
// НаименованиеОбъекта - Строка - наименование объекта, по которому.
//
Процедура СформироватьПоСтандартномуМакету(ТЧОтчета, Макет, ВерсияОбъекта, Знач ОписаниеВерсии, СсылкаНаОбъект) Экспорт
	
	МетаданныеОбъекта = СсылкаНаОбъект.Метаданные();
	
	НаименованиеОбъекта = МетаданныеОбъекта.Имя;
	
	ТЧОтчета = Новый ТабличныйДокумент;
	
	Если Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(СсылкаНаОбъект)) Тогда
		Макет = Справочники[НаименованиеОбъекта].ПолучитьМакет("МакетОбъекта");
	Иначе
		Макет = Документы[НаименованиеОбъекта].ПолучитьМакет("МакетОбъекта");
	КонецЕсли;
	
	// Заголовок
	Область = Макет.ПолучитьОбласть("Заголовок");
	ТЧОтчета.Вывести(Область);
	
	Область = ТЧОтчета.ПолучитьОбласть("R3");
	УстановитьСвойстваТекста(Область.Область("R1C2"), ОписаниеВерсии, , Истина);
	ТЧОтчета.Вывести(Область);
	
	Область = ТЧОтчета.ПолучитьОбласть("R5");
	ТЧОтчета.Вывести(Область);
	
	// Шапка
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Заполнить(ВерсияОбъекта);
	ТЧОтчета.Вывести(Шапка);
	
	Для Каждого МетаданныеТЧ Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		Если ВерсияОбъекта.ТабличныеЧасти[МетаданныеТЧ.Имя].Количество() > 0 Тогда
			Область = Макет.ПолучитьОбласть(МетаданныеТЧ.Имя+"Шапка");
			ТЧОтчета.Вывести(Область);
			
			ОбластьДеталиПриходаТовара = Макет.ПолучитьОбласть(МетаданныеТЧ.Имя);
			Для Каждого ТекСтрокаДеталиПриходаТовара Из ВерсияОбъекта.ТабличныеЧасти[МетаданныеТЧ.Имя] Цикл
				ОбластьДеталиПриходаТовара.Параметры.Заполнить(ТекСтрокаДеталиПриходаТовара);
				ТЧОтчета.Вывести(ОбластьДеталиПриходаТовара);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ВерсияОбъекта.Свойство("ТабличныеДокументы") Тогда
		ТабличныеДокументы = ВерсияОбъекта.ТабличныеДокументы;
		Если ТабличныеДокументы <> Неопределено Тогда
			Если Макет.Области.Найти("ШапкаТабличныхДокументов") <> Неопределено Тогда
				ШапкаТабличныхДокументов = Макет.ПолучитьОбласть("ШапкаТабличныхДокументов");
				ТЧОтчета.Вывести(ШапкаТабличныхДокументов);
				ШапкаТабличногоДокумента = ?(Макет.Области.Найти("ШапкаТабличногоДокумента") = Неопределено,
					Неопределено, Макет.ПолучитьОбласть("ШапкаТабличногоДокумента"));
				
				Для Каждого ЭлементСтруктуры Из ТабличныеДокументы Цикл
					Если ШапкаТабличногоДокумента <> Неопределено Тогда
						НаименованиеТабличногоДокумента = Новый Структура("НаименованиеТабличногоДокумента", ЭлементСтруктуры.Значение.Наименование);
						ШапкаТабличногоДокумента.Параметры.Заполнить(НаименованиеТабличногоДокумента);
						ТЧОтчета.Вывести(ШапкаТабличногоДокумента);
					КонецЕсли;
					ТЧОтчета.Вывести(ЭлементСтруктуры.Значение.Данные);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ТЧОтчета.ОтображатьСетку = Ложь;
	ТЧОтчета.Защита = Истина;
	ТЧОтчета.ТолькоПросмотр = Истина;
	ТЧОтчета.ОтображатьЗаголовки = Ложь;
	
КонецПроцедуры

// Выводит шапку отчета при выводе отчета по версии объекта.
//
Процедура ВывестиШапкуПоВерсии(ТЧОтчета, Знач Текст, Знач НомерСтроки, Знач НомерКолонки)
	
	Если Не ПустаяСтрока(Текст) Тогда
		
		ТЧОтчета.Область("C"+Строка(НомерКолонки)).ШиринаКолонки = 50;
		
		Регион = "R" + Формат(НомерСтроки, "ЧГ=0") + "C" + Формат(НомерКолонки, "ЧГ=0");
		ТЧОтчета.Область(Регион).Текст = Текст;
		ТЧОтчета.Область(Регион).ЦветФона = ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
		ТЧОтчета.Область(Регион).Шрифт = Новый Шрифт(, 8, Истина, , , );
		ТЧОтчета.Область(Регион).ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ТЧОтчета.Область(Регион).ГраницаСнизу  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ТЧОтчета.Область(Регион).ГраницаСлева  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ТЧОтчета.Область(Регион).ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит изменившиеся реквизиты в отчет. При этом получает их представление.
//
Функция ВывестиРеквизитыПоРазобранномуОбъекту(ТЧОтчета, ВерсияОбъекта, СсылкаНаОбъект) Экспорт
	
	Секция = ТЧОтчета.ПолучитьОбласть("R6");
	ВывестиТекстВОтчет(ТЧОтчета, Секция, "R1C1:R1C3", " ");
	ВывестиТекстВОтчет(ТЧОтчета, Секция, "R1C2", "Реквизиты", 11, Истина);
	ТЧОтчета.НачатьГруппуСтрок("ГруппаРеквизитов");
	ВывестиТекстВОтчет(ТЧОтчета, Секция, "R1C1:R1C3", " ");
	
	ЧислоВыводимыхСтрок = 0;
	
	Для Каждого ЭлементРеквизит Из ВерсияОбъекта.Реквизиты Цикл
		СтруктураОписанияНаименования = НаименованиеВыводимогоРеквизита(СсылкаНаОбъект, ЭлементРеквизит.НаименованиеРеквизита);
		Если Не СтруктураОписанияНаименования.ВыводитьРеквизит Тогда
			Продолжить;
		КонецЕсли;
		
		ВыводимоеНаименование = СтруктураОписанияНаименования.ВыводимоеНаименование;
		ОписаниеРеквизита = СтруктураОписанияНаименования.ОписаниеРеквизита;
		
		ЗначениеРеквизита = ?(ЭлементРеквизит.ЗначениеРеквизита = Неопределено, "", ЭлементРеквизит.ЗначениеРеквизита);
		ПредставлениеЗначения = ПредставлениеЗначенияРеквизита(ЗначениеРеквизита, ОписаниеРеквизита);
		
		УстановитьСвойстваТекста(Секция.Область("R1C2"), ВыводимоеНаименование, , Истина);
		УстановитьСвойстваТекста(Секция.Область("R1C3"), ПредставлениеЗначения);
		Секция.Область("R1C2:R1C3").ГраницаСнизу = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1, 0);
		Секция.Область("R1C2:R1C3").ЦветРамки = ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
		
		ТЧОтчета.Вывести(Секция);
		
		ЧислоВыводимыхСтрок = ЧислоВыводимыхСтрок + 1;
	КонецЦикла;
	
	ТЧОтчета.ЗакончитьГруппуСтрок();
	
	Возврат ЧислоВыводимыхСтрок;
	
КонецФункции

// Выводит табличные части по разобранному объекту, при выводе единственного объекта.
//
Функция ВывестиТабличныеЧастиПоРазобранномуОбъекту(ТЧОтчета, ВерсияОбъекта, НомерСтрокиВывода, СсылкаНаОбъект) Экспорт
	
	ЧислоВыводимыхСтрок = 0;
	
	Если ВерсияОбъекта.ТабличныеЧасти.Количество() <> 0 Тогда
		
		Для Каждого СтрокаТабличнаяЧасть Из ВерсияОбъекта.ТабличныеЧасти Цикл
			НаименованиеТабличнойЧасти = СтрокаТабличнаяЧасть.Ключ;
			ТабличнаяЧасть             = СтрокаТабличнаяЧасть.Значение;
			Если ТабличнаяЧасть.Количество() > 0 Тогда
				
				МетаданныеТЧ = СсылкаНаОбъект.Метаданные().ТабличныеЧасти.Найти(НаименованиеТабличнойЧасти);
				
				СинонимТЧ = НаименованиеТабличнойЧасти;
				Если МетаданныеТЧ <> Неопределено Тогда
					СинонимТЧ = МетаданныеТЧ.Представление();
				КонецЕсли;
				
				Секция = ТЧОтчета.ПолучитьОбласть("R" + Строка(НомерСтрокиВывода));
				ВывестиТекстВОтчет(ТЧОтчета, Секция, "R1C1:R1C100", " ");
				ОбластьВывода = ВывестиТекстВОтчет(ТЧОтчета, Секция, "R1C2", СинонимТЧ, 11, Истина);
				ТЧОтчета.Область("R" + Формат(ОбластьВывода.Верх, "ЧГ=0") + "C2").СоздатьФорматСтрок();
				ТЧОтчета.Область("R" + Формат(ОбластьВывода.Верх, "ЧГ=0") + "C2").ШиринаКолонки = Окр(СтрДлина(СинонимТЧ)*2, 0, РежимОкругления.Окр15как20);
				ТЧОтчета.НачатьГруппуСтрок("ГруппаСтрок");
				
				ВывестиТекстВОтчет(ТЧОтчета, Секция, "R1C1:R1C3", " ");
				
				ЧислоВыводимыхСтрок = ЧислоВыводимыхСтрок + 1;
				
				НомерСтрокиВывода = НомерСтрокиВывода + 3;
				
				ДобавляемаяТЧ = Новый ТабличныйДокумент;
				
				ДобавляемаяТЧ.Присоединить(СформироватьПустойСектор(ТабличнаяЧасть.Количество()+1));
				
				НомерКолонки = 2;
				
				СоответствиеРазмерностиКолонок = Новый Соответствие;
				
				Секция = Новый ТабличныйДокумент;
				ОбластьСекции = Секция.Область("R1C1");
				УстановитьСвойстваТекста(ОбластьСекции, "N", , Истина, Истина);
				ОбластьСекции.ЦветФона = ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет;
				
				НомерСтроки = 1;
				Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
					НомерСтроки = НомерСтроки + 1;
					УстановитьСвойстваТекста(Секция.Область("R" + Формат(НомерСтроки, "ЧГ=0") + "C1"), Формат(НомерСтроки-1, "ЧГ=0"), , Ложь, Истина);
				КонецЦикла;
				ДобавляемаяТЧ.Присоединить(Секция);
				
				НомерКолонки = 3;
				
				Для Каждого КолонкаТабличнойЧасти Из ТабличнаяЧасть.Колонки Цикл
					Секция = Новый ТабличныйДокумент;
					НаименованиеПоля = КолонкаТабличнойЧасти.Имя;
					
					ОписаниеПоля = Неопределено;
					Если МетаданныеТЧ <> Неопределено Тогда
						ОписаниеПоля = МетаданныеТЧ.Реквизиты.Найти(НаименованиеПоля);
					КонецЕсли;
					
					Если ОписаниеПоля = Неопределено Или Не ЗначениеЗаполнено(ОписаниеПоля.Синоним) Тогда
						ВыводимоеНаименованиеПоля = НаименованиеПоля;
					Иначе
						ВыводимоеНаименованиеПоля = ОписаниеПоля.Синоним;
					КонецЕсли;
					ЦветЗаголовкаКолонки = ?(ОписаниеПоля = Неопределено, ЦветаСтиля.ЗаголовокУдаленногоРеквизитаФон, ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
					СекцияОбласти = Секция.Область("R1C1");
					УстановитьСвойстваТекста(СекцияОбласти, ВыводимоеНаименованиеПоля, , Истина, Истина);
					СекцияОбласти.ЦветФона = ЦветЗаголовкаКолонки;
					СоответствиеРазмерностиКолонок.Вставить(НомерКолонки, СтрДлина(НаименованиеПоля) + 4);
					НомерСтроки = 1;
					Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
						НомерСтроки = НомерСтроки + 1;
						Значение = ?(СтрокаТабличнойЧасти[НаименованиеПоля] = Неопределено, "", СтрокаТабличнойЧасти[НаименованиеПоля]);
						ПредставлениеЗначения = ПредставлениеЗначенияРеквизита(Значение, ОписаниеПоля);
						
						УстановитьСвойстваТекста(Секция.Область("R" + Формат(НомерСтроки, "ЧГ=0") + "C1"), ПредставлениеЗначения, , , Истина);
						Если СтрДлина(ПредставлениеЗначения) > (СоответствиеРазмерностиКолонок[НомерКолонки] - 4) Тогда
							СоответствиеРазмерностиКолонок[НомерКолонки] = СтрДлина(ПредставлениеЗначения) + 4;
						КонецЕсли;
					КонецЦикла;
					
					ДобавляемаяТЧ.Присоединить(Секция);
					НомерКолонки = НомерКолонки + 1;
				КонецЦикла;
				
				ОбластьВывода = ТЧОтчета.Вывести(ДобавляемаяТЧ);
				ТЧОтчета.Область(ОбластьВывода.Верх, 1, ОбластьВывода.Низ, НомерКолонки).СоздатьФорматСтрок();
				ТЧОтчета.Область("R" + Формат(ОбластьВывода.Верх, "ЧГ=0") + "C2").ШиринаКолонки = 7;
				Для ТекущийНомерКолонки = 3 По НомерКолонки-1 Цикл
					ТЧОтчета.Область("R" + Формат(ОбластьВывода.Верх, "ЧГ=0") + "C" + Формат(ТекущийНомерКолонки, "ЧГ=0")).ШиринаКолонки = СоответствиеРазмерностиКолонок[ТекущийНомерКолонки];
				КонецЦикла;
				ТЧОтчета.ЗакончитьГруппуСтрок();
				
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецФункции

// Выводит текст в область табличного документа с определенным оформлением.
//
Функция ВывестиТекстВОтчет(ТаблицаОтчета, Знач Секция, Знач Регион, Знач Текст, Знач Размер = 9, Знач Жирный = Ложь)
	
	ОбластьСекции = Секция.Область(Регион);
	
	ОбластьСекции.Текст      = Текст;
	ОбластьСекции.Шрифт      = Новый Шрифт(, Размер, Жирный, , , );
	ОбластьСекции.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Лево;
	
	ОбластьСекции.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	ОбластьСекции.ГраницаСнизу  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	ОбластьСекции.ГраницаСлева  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	ОбластьСекции.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.НетЛинии);
	
	Возврат ТаблицаОтчета.Вывести(Секция);
	
КонецФункции

// Используется для вывода текста в область табличного документа
// с условным оформлением.
//
Процедура УстановитьСвойстваТекста(ОбластьСекции, Текст, Знач Размер = 9, Знач Жирный = Ложь, Знач ПоказыватьГраницы = Ложь)
	
	ОбластьСекции.Текст = Текст;
	ОбластьСекции.Шрифт = Новый Шрифт(, Размер, Жирный, , , );
	
	Если ПоказыватьГраницы Тогда
		ОбластьСекции.ГраницаСверху = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ОбластьСекции.ГраницаСнизу  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ОбластьСекции.ГраницаСлева  = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ОбластьСекции.ГраницаСправа = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная);
		ОбластьСекции.ГоризонтальноеПоложение = ГоризонтальноеПоложение.Центр;
	КонецЕсли;
	
КонецПроцедуры

// Формирует пустой сектор для вывода в отчет. Используется,
// если строка не была изменена в одной из версий.
//
Функция СформироватьПустойСектор(Знач ЧислоСтрок, Знач ТипВывода = "")
	
	ЗначениеЗаполнения = Новый Массив;
	
	Для Индекс = 1 По ЧислоСтрок Цикл
		ЗначениеЗаполнения.Добавить(" ");
	КонецЦикла;
	
	Возврат СформироватьСекторСтрокиТЧ(ЗначениеЗаполнения, ТипВывода);
	
КонецФункции

// ЗначениеЗаполнения - массив строк.
// ТипВывода - строка :
//           "и" - изменение
//           "д" - добавление
//           "у" - удаление
//           ""  - обычный вывод
Функция СформироватьСекторСтрокиТЧ(Знач ЗначениеЗаполнения,Знач ТипВывода = "")
	
	ОбщийШаблон = РегистрыСведений.ВерсииОбъектов.ПолучитьМакет("СтандартныйМакетПредставленияОбъекта");
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Если      ТипВывода = ""  Тогда
		Шаблон = ОбщийШаблон.ПолучитьОбласть("ИсходноеЗначениеРеквизита");
	ИначеЕсли ТипВывода = "И" Тогда
		Шаблон = ОбщийШаблон.ПолучитьОбласть("ИзмененноеЗначениеРеквизита");
	ИначеЕсли ТипВывода = "Д" Тогда
		Шаблон = ОбщийШаблон.ПолучитьОбласть("ДобавленныйРеквизит");
	ИначеЕсли ТипВывода = "У" Тогда
		Шаблон = ОбщийШаблон.ПолучитьОбласть("УдаленныйРеквизит");
	КонецЕсли;
	
	Для Каждого ОчередноеЗначение Из ЗначениеЗаполнения Цикл
		Шаблон.Параметры.ЗначениеРеквизита = ОчередноеЗначение;
		ТабличныйДокумент.Вывести(Шаблон);
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПредставлениеЗначенияРеквизита(ЗначениеРеквизита, ОбъектМетаданныхРеквизит)
	
	ФорматнаяСтрока = "";
	Если ОбъектМетаданныхРеквизит <> Неопределено Тогда
		Если ТипЗнч(ЗначениеРеквизита) = Тип("Дата") Тогда
			ФорматнаяСтрока = "ДЛФ=DT";
			Если ОбъектМетаданныхРеквизит.Тип.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Дата Тогда
				ФорматнаяСтрока = "ДЛФ=D";
			ИначеЕсли ОбъектМетаданныхРеквизит.Тип.КвалификаторыДаты.ЧастиДаты = ЧастиДаты.Время Тогда
				ФорматнаяСтрока = "ДЛФ=T";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Формат(ЗначениеРеквизита, ФорматнаяСтрока);
	
КонецФункции

Функция РазборВерсии(Ссылка, НомерВерсии) Экспорт
	
	СведенияОВерсии = СведенияОВерсииОбъекта(Ссылка, НомерВерсии);
	
	Результат = РазборПредставленияОбъектаXML(СведенияОВерсии.ВерсияОбъекта, Ссылка);
	Результат.Вставить("ИмяОбъекта",     Строка(Ссылка));
	Результат.Вставить("АвторИзменения", СокрЛП(Строка(СведенияОВерсии.АвторВерсии)));
	Результат.Вставить("ДатаИзменения",  СведенияОВерсии.ДатаВерсии);
	Результат.Вставить("Комментарий",    СведенияОВерсии.Комментарий);
	Результат.Вставить("ТабличныеДокументы", СведенияОВерсии.ТабличныеДокументы);
	ВерсионированиеОбъектовПереопределяемый.ПослеРазбораВерсииОбъекта(Ссылка, Результат);
	
	Возврат Результат;
	
КонецФункции

// Выводит табличные документы по разобранному объекту, при выводе единственного объекта.
Процедура ВывестиТабличныеДокументыПоРазобранномуОбъекту(ТабличныйДокумент, ОписаниеОбъекта)
	
	ТабличныеДокументы = ОписаниеОбъекта.ТабличныеДокументы;
	
	Если ТабличныеДокументы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщийШаблон = РегистрыСведений.ВерсииОбъектов.ПолучитьМакет("СтандартныйМакетПредставленияОбъекта");
	
	ШаблонЗаголовокТабличныеДокументы = ОбщийШаблон.ПолучитьОбласть("ШапкаТабличныхДокументов");	
	ШаблонСтрокаТабличныеДокументы = ОбщийШаблон.ПолучитьОбласть("ШапкаТабличногоДокумента");
	ШаблонПустаяСтрока = ОбщийШаблон.ПолучитьОбласть("СвободнаяСтрока");
	
	ТабличныйДокумент.Вывести(ШаблонПустаяСтрока);
	ТабличныйДокумент.Вывести(ШаблонЗаголовокТабличныеДокументы);
	ТабличныйДокумент.Вывести(ШаблонПустаяСтрока);
	ТабличныйДокумент.НачатьГруппуСтрок("ГруппаТабличныхДокументов");
	
	Для Каждого ЭлементСтруктуры Из ТабличныеДокументы Цикл
		НаименованиеТабличногоДокумента = ЭлементСтруктуры.Значение.Наименование;
		ШаблонСтрокаТабличныеДокументы.Параметры.НаименованиеТабличногоДокумента = НаименованиеТабличногоДокумента;
		ТабличныйДокумент.Вывести(ШаблонСтрокаТабличныеДокументы);
		ТабличныйДокумент.Вывести(ШаблонПустаяСтрока);
		
		ВыводимыйДокумент = ЭлементСтруктуры.Значение.Данные;
		ОбластьВыводаТабличногоДокумента = ТабличныйДокумент.Вывести(ВыводимыйДокумент);
		ОбластьВыводаТабличногоДокумента.СоздатьФорматСтрок();
		
		Для НомерКолонки = 1 По ВыводимыйДокумент.ШиринаТаблицы Цикл 
			ШиринаКолонки = ВыводимыйДокумент.Область(1, НомерКолонки, ВыводимыйДокумент.ВысотаТаблицы, НомерКолонки).ШиринаКолонки;
			ТабличныйДокумент.Область(ОбластьВыводаТабличногоДокумента.Верх, НомерКолонки,
				ОбластьВыводаТабличногоДокумента.Низ, НомерКолонки).ШиринаКолонки = ШиринаКолонки;
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ШаблонПустаяСтрока);
	КонецЦикла;
	
	ТабличныйДокумент.ЗакончитьГруппуСтрок();
	ТабличныйДокумент.Вывести(ШаблонПустаяСтрока);
	
КонецПроцедуры

Функция НаименованиеВыводимогоРеквизита(СсылкаНаОбъект, Знач ИмяРеквизита) Экспорт
	
	ВыводитьРеквизит = Истина;
	
	ИмяРеквизита = ПолучитьПредставлениеРеквизитаНаЯзыке(ИмяРеквизита);
	ОписаниеРеквизита = СсылкаНаОбъект.Метаданные().Реквизиты.Найти(ИмяРеквизита);
	
	Если ОписаниеРеквизита = Неопределено Тогда
		Для Каждого ОписаниеСтандартногоРеквизита Из СсылкаНаОбъект.Метаданные().СтандартныеРеквизиты Цикл
			Если ОписаниеСтандартногоРеквизита.Имя = ИмяРеквизита Тогда
				ОписаниеРеквизита = ОписаниеСтандартногоРеквизита;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ОписаниеРеквизита = Неопределено Тогда
		ОписаниеРеквизита = Метаданные.ОбщиеРеквизиты.Найти(ИмяРеквизита);
	КонецЕсли;
	
	ПредставлениеРеквизита = ИмяРеквизита;
	Если ОписаниеРеквизита <> Неопределено Тогда
		ПредставлениеРеквизита = ОписаниеРеквизита.Представление();
	КонецЕсли;
	
	ВерсионированиеОбъектовПереопределяемый.ПриОпределенииНаименованияРеквизитаОбъекта(СсылкаНаОбъект, 
		ИмяРеквизита, ПредставлениеРеквизита, ВыводитьРеквизит);
	
	Возврат Новый Структура("ВыводимоеНаименование, ВыводитьРеквизит, ОписаниеРеквизита", 
		ПредставлениеРеквизита, ВыводитьРеквизит, ОписаниеРеквизита);
	
КонецФункции

Функция ДанныеДляХранения(СсылкаНаОбъект)
	
	ДанныеОбъекта = СериализоватьОбъект(СсылкаНаОбъект.ПолучитьОбъект());
	ТабличныеДокументы = ТабличныеДокументыОбъекта(СсылкаНаОбъект);
	
	Если ТабличныеДокументы = Неопределено Или ТабличныеДокументы.Количество() = 0 Тогда
		Результат = ДанныеОбъекта; // ДвоичныеДанные
	Иначе
		Результат = Новый Структура;
		Результат.Вставить("Объект", ДанныеОбъекта);
		Результат.Вставить("ТабличныеДокументы", ТабличныеДокументы);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТабличныеДокументыОбъекта(Ссылка)
	Результат = Новый Структура;
	ВерсионированиеОбъектовПереопределяемый.ПриПолученииТабличныхДокументовОбъекта(Ссылка, Результат);
	Возврат Результат;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Обработчики служебных событий подсистем БСП.

// Заполняет массив списком имен объектов метаданных, данные которых могут содержать ссылки на различные объекты
// метаданных, но при этом эти ссылки не должны учитываться в бизнес-логике приложения.
//
// Параметры:
//  Массив       - массив строк, например, "РегистрСведений.ВерсииОбъектов".
//
Процедура ПриДобавленииИсключенийПоискаСсылок(Массив) Экспорт
	
	Массив.Добавить(Метаданные.РегистрыСведений.ВерсииОбъектов.ПолноеИмя());
	
КонецПроцедуры

// Процедура является обработчиком одноименного события, возникающего при обмене данными в распределенной
// информационной базе.
//
// Параметры:
// см. описание обработчика события ПриПолученииДанныхОтПодчиненного() в синтаксис-помощнике.
// 
Процедура ПриПолученииДанныхОтПодчиненного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад, Отправитель) Экспорт
	
	ДобавлениеСведенийОВерсииОбъектаПриОбменеДанными(ЭлементДанных, Отправитель);
	
КонецПроцедуры

// Процедура является обработчиком одноименного события, возникающего при обмене данными в распределенной
// информационной базе.
//
// Параметры:
// см. описание обработчика события ПриПолученииДанныхОтГлавного() в синтаксис-помощнике.
// 
Процедура ПриПолученииДанныхОтГлавного(ЭлементДанных, ПолучениеЭлемента, ОтправкаНазад, Отправитель) Экспорт
	
	ДобавлениеСведенийОВерсииОбъектаПриОбменеДанными(ЭлементДанных, Отправитель);
	
КонецПроцедуры

#КонецОбласти
