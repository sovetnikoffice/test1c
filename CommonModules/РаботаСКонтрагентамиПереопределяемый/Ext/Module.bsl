////////////////////////////////////////////////////////////////////////////////
// Работа с контрагентами.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПроверкаКонтрагентовВДокументах

// Процедура - Формирование описания расположения счета-фактуры в документе.
//
// Параметры:
//  Форма	 - УправляемаяФорма - Форма документа-основания, для которого необходимо получить описание счета-фактуры.
//  Описание - ТаблицаЗначений - Таблица, в которую помещается описание счета-фактуры.
//				Структура колонок указана в функции ШаблонТаблицыОписания модуля ПроверкаКонтрагентов.
Процедура СформироватьОписаниеДокументовСоСчетомФактуройВПодвале(Форма, Описание) Экспорт
	
	// _Демо начало примера
	
	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСоСчетомФактуройВПодвале(Форма) Тогда
		
		СчетФактура = ПроверкаКонтрагентовКлиентСервер.СчетФактура(Форма);
		
		Если ЗначениеЗаполнено(СчетФактура) Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписание(
				Описание,
				СчетФактура, 
				Форма.Элементы.СчетФактура,
				Неопределено,
				Форма.Элементы.ГруппаСчетФактура);
				
		КонецЕсли;
			
	КонецЕсли;
	
	// _Демо конец примера
		
КонецПроцедуры

// Процедура - Формирование описания расположения контрагента, находящегося в шапке документа.
//
// Параметры:
//  Форма	 - УправляемаяФорма - Форма документа, для которой необходимо получить описание расположения контрагента.
//  Описание - ТаблицаЗначений - Таблица, в которую помещается описание контрагентов.
//				Структура колонок указана в функции ШаблонТаблицыОписания модуля ПроверкаКонтрагентов.
Процедура СформироватьОписаниеДокументовСКонтрагентомВШапке(Форма, Описание) Экспорт
	
	// _Демо начало примера

	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	// Заполнение данных по контрагенту в шапке.
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСКонтрагентомВШапке(Форма) Тогда
		
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание, 
			ДокументСсылка, 
			Форма.Элементы.Контрагент,
			ДокументОбъект.Контрагент,
			Форма.Элементы.ГруппаКонтрагент);
			
	КонецЕсли;
		
	// _Демо конец примера
		
КонецПроцедуры

// Процедура - Формирование описания  расположения контрагентов, находящихся в табличной части документа.
//
// Параметры:
//  Форма	 - УправляемаяФорма - Форма документа, для которой необходимо получить описание.
//  Описание - ТаблицаЗначений - Таблица с описанием расположения контрагентов в табличной части документа.
//				Структура колонок указана в функции ШаблонТаблицыОписанияТабличныхЧастей модуля ПроверкаКонтрагентов.
Процедура СформироватьОписаниеДокументовСТабличнымиЧастями(Форма, Описание) Экспорт
	
	// _Демо начало примера

	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоДокументСКонтрагентомВТабличнойЧасти(Форма) Тогда
		
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка._ДемоПоступлениеТоваров") Тогда
			
			ПроверкаКонтрагентов.ДополнитьОписаниеТабличныхЧастей(
				Форма, Описание, "АгентскиеУслуги", "АгентскиеУслугиКонтрагент", "АгентскиеУслугиДатаСобытия");
				
		КонецЕсли;
		
	КонецЕсли;
	
	// _Демо конец примера
	
КонецПроцедуры

// Процедура - Формирование описания расположения контрагентов в счете-фактуре.
//
// Параметры:
//  Форма	 - УправляемаяФорма - Форма счета-фактуры, для которой необходимо получить описание.
//  Описание - ТаблицаЗначений - Таблица, в которую помещается описание контрагентов.
//				Структура колонок указана в функции ШаблонТаблицыОписания модуля ПроверкаКонтрагентов.
Процедура СформироватьОписаниеСчетовФактур(Форма, Описание) Экспорт
	
	// _Демо начало примера

	ДокументОбъект = Форма.Объект;
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	// Заполнение данных для счета-фактуры.
	Если ПроверкаКонтрагентовКлиентСервер.ЭтоСчетФактура(Форма) Тогда
		
		Ссылка 			= ДокументСсылка;
		ТипДокумента	= ТипЗнч(Ссылка);
		
		// Контрагент
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание, 
			Ссылка,
			Форма.Элементы.Контрагент,
			ДокументОбъект.Контрагент,
			Форма.Элементы.ГруппаКонтрагент);
			
		// Комитент
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание,  
			Ссылка,
			Форма.Элементы.Комитент,
			ДокументОбъект.Комитент,
			Форма.Элементы.ГруппаКомитент);
				
		// Продавец
		ПроверкаКонтрагентов.ДополнитьОписание(
			Описание,  
			Ссылка,
			Форма.Элементы.Продавец,
			ДокументОбъект.Продавец,
			Форма.Элементы.ГруппаПродавец);
		
	КонецЕсли;
	
	// _Демо конец примера
		
КонецПроцедуры

// Процедура - Возвращает массив имен реквизитов счета-фактуры, содержащих контрагентов.
//
// Параметры:
//  Тип	 			- Тип - Тип счета-фактуры, для которой необходимо получить описание.
//  ИменаРеквизитов	- Массив - массив имен(строк) реквизитов счета-фактуры, содержащих контрагентов.
Процедура ПолучитьИменаРеквизитовКонтрагентовВСчетеФактуре(Тип, ИменаРеквизитов) Экспорт
	
	// _Демо начало примера
	
	ИменаРеквизитов.Добавить("Контрагент");
	ИменаРеквизитов.Добавить("Комитент");
	ИменаРеквизитов.Добавить("Продавец");
	
	// _Демо конец примера

КонецПроцедуры

// Процедура - Получение даты документа, в котором используется проверка контрагентов.
// 		Проверка контрагентов будет выполняться по состоянию на эту дату. 
//
// Параметры:
//  ДокументОбъект	 - ДокументОбъект - Документ, в котором используется проверка контрагентов.
//  Дата			 - Дата - Результат получения даты документа.
Процедура ОпределитьДатуВДокументе(ДокументОбъект, Дата) Экспорт
	
	// _Демо начало примера

	Дата = НачалоДня(ДокументОбъект.Дата);
	
	// _Демо конец примера
	
КонецПроцедуры

// Процедура - Определение даты в счете-фактуре.
//
// Параметры:
//  СчетФактураОбъект	 - ДанныеФормыСтруктура или ДокументОбъект - Счет-фактура, чью дату необходимо получить.
//  Дата				 - Дата - Результат, дата счета-фактуры.
Процедура ОпределитьДатуВСчетеФактуре(СчетФактураОбъект, Дата) Экспорт
	
	// _Демо начало примера

	Дата = НачалоДня(СчетФактураОбъект.Дата);
	
	// _Демо конец примера
	
КонецПроцедуры

// Процедура - Возможность переопределить стандартное заполнение данных о контрагентах в счет-фактурах, созданных на основании документа
//
// Параметры:
//  СтандартнаяОбработка - Булево - необходимо установить в Ложь, если стандартная обработка переопределяется.
//  ДокументОбъект       - Произвольный - данные содержащие информацию о счет-фактурах.
//  ДанныеКонтрагентов   - ТаблицаЗначений - подготавливаемая таблица с данными о контрагентах
//  Форма                - УправляемаяФорма, Неопределено - Форма из которой выполняется вызов.
//
Процедура ЗаполнитьДанныеКонтрагентовВСчетеФактуре(СтандартнаяОбработка, ДокументОбъект, ДанныеКонтрагентов, Форма) Экспорт
	
	
	
КонецПроцедуры

// Процедура - Возможность переопределить стандартное сохранение результатов проверки контрагентов счет-фактур, выписанных на основании
//             документа основания в форме документа основания 
//
// Параметры:
//  СтандартнаяОбработка - Булево - необходимо установить в Ложь, если стандартная обработка переопределяется.
//  ДанныеКонтрагентов   - ТаблицаЗначений - подготавливаемая таблица с данными о контрагентах
//  Форма                - УправляемаяФорма, Неопределено - Форма из которой выполняется вызов.
//
Процедура ЗапомнитьРезультатПроверкиКонтрагентовСчетФактурыВДокументе(СтандартнаяОбработка, ДанныеКонтрагентов, Форма) Экспорт
	
	
	
КонецПроцедуры

// Процедура - Возможность дополнить структуру РеквизитыПроверкиКонтрагентов при создании в форме документа
//
// Параметры:
//   Форма          - УправляемаяФорма, Неопределено - Форма из которой выполняется вызов.
//   НовыеРеквизиты - Структура - сформированная структура дополнительных реквизитов документа, которая может быть дополнена.
//
Процедура ДополнитьРеквизитыФормыДокумент(Форма, НовыеРеквизиты) Экспорт


КонецПроцедуры

// Процедура - Возможность переопределить сохраняемые результаты проверки контрагентов в документе.
//
// Параметры:
//  СтандартнаяОбработка - Булево - необходимо установить в Ложь, если стандартная обработка переопределяется.
//  СохраняемоеЗначение  - Соответствие - сохраняемые данные
//  Форма                - УправляемаяФорма, Неопределено - Форма из которой выполняется вызов.
//
Процедура СохраняемыеРезультатыПроверкиСчетФактурыВДокументе(СтандартнаяОбработка, СохраняемоеЗначение, Форма) Экспорт
	
	
	
КонецПроцедуры

// Процедура - Отображения результата проверки контрагента в документе.
//
// Параметры:
//  Форма	 				- УправляемаяФорма - Форма документа, для которого выводится результат проверки контрагента.
//		Результат проверки хранится в реквизите РеквизитыПроверкиКонтрагентов(Структура) формы документа.
//		Структуру полей РеквизитыПроверкиКонтрагентов см. процедуре ИнициализироватьРеквизитыФормыДокумент ОМ
//		ПроверкаКонтрагентов.
//  СтандартнаяОбработка	- Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                  процедуре.
Процедура ОтобразитьРезультатПроверкиКонтрагентаВДокументе(Форма, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Процедура - Прорисовать состояния контрагентов в документе.
//
// Параметры:
//  Форма				 	 - УправляемаяФорма - Форма документа, в котором выполняется проверка контрагентов.
//  СостояниеПроверки		 - Перечисления.СостоянияПроверкиКонтрагентов - Указывает, в каком состояние проверка:
//		завершилась, не завершилась, выполняется или отсутствует доступ к веб-сервису.
//  ДополнительныеПараметры	 - Неопределено или Структура - Параметры, показывающие в каком элементе формы произошло
//                             изменение и это изменение вызвало запуск проверки контрагента. Описание структуры см в
//                             ПроверкаКонтрагентовКлиентСервер.ПараметрыФоновогоЗадания.
//  СтандартнаяОбработка	 - Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                   процедуре.
//		Пример см в ПроверкаКонтрагентов.ПрорисоватьСостоянияКонтрагентовВДокументе.
Процедура ПрорисоватьСостоянияКонтрагентовВДокументе(Форма, СостояниеПроверки, ДополнительныеПараметры = Неопределено, СтандартнаяОбработка = Истина) Экспорт
	
КонецПроцедуры

// Процедура - Установка условного оформления в форме выбора или списка документов.
//
// Параметры:
//  Список				 	 - ДинамическийСписок - Динамический список формы выбора или списка документов.
//  СтандартнаяОбработка	 - Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                   процедуре.
Процедура УстановитьУсловноеОформлениеСпискаДокументов(Список, СтандартнаяОбработка = Истина) Экспорт
	
КонецПроцедуры

// Процедура - Установка условного оформления в табличной части документа.
//
// Параметры:
//  Форма				 	 - УправляемаяФорма - Форма документа, в котором выполняется проверка контрагентов.
//  СтандартнаяОбработка	 - Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                   процедуре.
Процедура УстановитьУсловноеОформлениеТабличнойЧастиДокумента(Форма, СтандартнаяОбработка = Истина) Экспорт
	
КонецПроцедуры

// Процедура - Добавление колонки с результатом проверки контрагента в таблицу формы документа.
//
// Параметры:
//  Форма				 	 - УправляемаяФорма - Форма документа, в котором выполняется проверка контрагентов.
//  СтандартнаяОбработка	 - Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                   процедуре.
Процедура ДобавитьКолонкиТаблицыФормыСРезультатамиПроверкиВТабличныеЧасти(Форма, СтандартнаяОбработка = Истина) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаКонтрагентовВОтчетах

// Процедура - Выделение контрагента с ошибкой в строке отчета.
//
// Параметры:
//  ТабличныйДокумент	 - ТабличныйДокумент - Табличный документ отчета, в котором выделяется строка с некорректным
//                                           контрагентом.
//  НомерСтроки			 - Число - Номер строки табличного документа, в которой находится некорректный контрагент.
Процедура ВыделитьКонтрагентаСОшибкойВСтрокеОтчета(ТабличныйДокумент, НомерСтроки) Экспорт
	
	// _Демо начало примера
	Область = ТабличныйДокумент.Область(НомерСтроки, 1, НомерСтроки, ТабличныйДокумент.ШиринаТаблицы);
	Область.ЦветФона = Новый Цвет(251, 212, 212);
	// _Демо конец примера
	
КонецПроцедуры

// Процедура - Предназначена для формирования отчета, в котором используется проверка контрагентов, из общего модуля.
//
// Параметры:
//  ПараметрыФормированияОтчета	 - Структура - Параметры, с которыми необходимо сформировать отчет.
//  Отчет						 - ТабличныйДокумент - результат формирования отчета.
Процедура СформироватьОтчет(ПараметрыФормированияОтчета, Отчет) Экспорт
	
	// _Демо начало примера
	Если ПараметрыФормированияОтчета.Свойство("ВидОтчета") И ПараметрыФормированияОтчета.ВидОтчета = Отчеты._ДемоКнигаПокупок Тогда
		
		ВременныйАдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
		Отчеты._ДемоКнигаПокупок.СформироватьОтчет(ПараметрыФормированияОтчета, ВременныйАдресХранилища);
		Результат = ПолучитьИзВременногоХранилища(ВременныйАдресХранилища);
		
		Отчет = Результат.РезультатФормированияОтчета;
		
	КонецЕсли;
	// _Демо конец примера
	
КонецПроцедуры 

// Процедура - Вывод результата проверки контрагента в поле табличного документа.
//
// Параметры:
//  Форма				 		 - УправляемаяФорма - Форма отчета, в котором выполняется проверка контрагентов.
//  ПолеТабличногоДокумента		 - ПолеФормы с видом ПолеТабличногоДокумента - поле, в которое необходимо выводить отчет.
//  РезультатФормированияОтчета	 - ТабличныйДокумент - Документ, предназначенный для вывода в поле формы.
//  СтандартнаяОбработка		 - Булево - Определяет, выполнять ли действие, определенное в данной процедуре или выполнить
//                                    стандартную обработку.
Процедура ВывестиОтчет(Форма, ПолеТабличногоДокумента, РезультатФормированияОтчета, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Процедура - Определяет, нужно ли выводить данную строку в отчете по некорректным контрагентам.
// 	Пример см. в ПроверкаКонтрагентов.ВыводитьСтрокуОтчета.
//
// Параметры:
//  СтруктураПараметров	 - Структура - Параметры формирования отчета. 
//		Для проверки контрагентов в отчете важно наличие ключа "ДанныеДляПроверкиКонтрагентов".
//		Данный ключ формируется в процедуре ДобавитьПараметрыДляПроверкиКонтрагентов.
//  Контрагент				- СправочникСсылка - Проверяемый контрагент в строке отчета.
//  ИНН						- Строка - ИНН контрагента.
//  КПП						- Строка - ИНН контрагента.
//  Дата					- Дата - Дата, на которую необходимо выполнять проверку контрагента.
//  СтандартнаяОбработка	- Булево - Определяет, выполнять ли действие, определенное в данной процедуре или выполнить
//                                  стандартную обработку.
//  Результат				- Булево - Определяет, нужно ли выводить строку.
Процедура ОпределитьНужноЛиВыводитьСтрокуОтчета(СтруктураПараметров, Контрагент, ИНН, КПП, Дата, Результат, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Процедура - Отображения результата проверки контрагента в отчете.
//
// Параметры:
//  Форма	 				- УправляемаяФорма - Форма отчета, для которого выводится результат проверки контрагента.
//		Результат проверки хранится в реквизите РеквизитыПроверкиКонтрагентов(Структура),
//		ПроверкаКонтрагентовНедействующиеКонтрагенты формы отчета.
//		Структуру полей РеквизитыПроверкиКонтрагентов см. процедуре ИнициализироватьРеквизитыФормыОтчета ОМ
//		ПроверкаКонтрагентов.
//  СтандартнаяОбработка	- Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                  процедуре.
Процедура ОтобразитьРезультатПроверкиКонтрагентаВОтчете(Форма, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

// Процедура - Установка условного оформления в форме выбора или списка справочника контрагентов.
//
// Параметры:
//  Список				 	 - ДинамическийСписок - Динамический список формы выбора или списка справочника контрагентов.
//  СтандартнаяОбработка	 - Булево - Если Ложь, то игнорируется стандартное действие и выполняется указанное в данной
//                                   процедуре.
Процедура УстановитьУсловноеОформлениеСпискаКонтрагентов(Список, СтандартнаяОбработка = Истина) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаКонтрагентовВСправочнике

// Процедура - Определяется, является ли контрагент иностранным.
//
// Параметры:
//  ДанныеКонтрагента   - СтрокаТаблицыЗначений - Содержит информацию о контрагенте. 
//      Подробнее о колонках таблицы можно узнать в описании к процедуре ПроверкаКонтрагентов.ШаблонТаблицыДанныеКонтрагента()
//  ЯвляетсяИностранным - Булево - Результат проверки.
Процедура ОпределитьЯвляетсяЛиКонтрагентИностранным(ДанныеКонтрагента, ЯвляетсяИностранным) Экспорт
	
	// _Демо начало примера
	КонтрагентСсылка = ДанныеКонтрагента.Контрагент;
	
	Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(КонтрагентСсылка)) И КонтрагентСсылка <> Справочники._ДемоКонтрагенты.ПустаяСсылка() Тогда
		ЯвляетсяИностранным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтрагентСсылка, "ЭтоИностранныйКонтрагент");
	Иначе
		ЯвляетсяИностранным = КонтрагентСсылка.ЭтоИностранныйКонтрагент;
	КонецЕсли;
	// _Демо конец примера
	
КонецПроцедуры

// Возможность дополнить структуру РеквизитыПроверкиКонтрагентов для формы справочника 
//
// Параметры:
//  Форма  - УправляемаяФорма - форма, для которой дополняется структура РеквизитыПроверкиКонтрагентов
//  НовыеРеквизиты  - Структура - структура, которая транслируется в реквизит формы РеквизитыПроверкиКонтрагентов
//
Процедура ДополнитьРеквизитыФормыКонтрагент(Форма, НовыеРеквизиты) Экспорт
	
	// _Демо начало примера
	
	НовыеРеквизиты.Вставить("НеИспользоватьКэш", Ложь);
	НовыеРеквизиты.Вставить("ЮридическоеФизическоеЛицо", Неопределено);

	Если Форма.ИмяФормы = "Справочник._ДемоКонтрагенты.Форма.ФормаЭлемента" Тогда
		НовыеРеквизиты.ЮридическоеФизическоеЛицо = Форма.Объект.ЮридическоеФизическоеЛицо;
	КонецЕсли;
	
	// _Демо конец примера
	
КонецПроцедуры

// Определяет, является ли контрагент физическим или юридическим лицом 
//
// Параметры:
//  СтандартнаяОбработка  - Булево - если стандартный механим переопределяется, то должно быть установлено значение "Ложь".
//  ЭтоЮрЛицо             - Булево - Истина, если юридическое лицо, и Ложь, если физическое.
//  ДанныеКонтрагента     - Структура - структура, которая содержит данные обрабатываемого контрагента.
//
Процедура ОпределитьЮрИлиФизЛицоКонтрагент(СтандартнаяОбработка, ЭтоЮрЛицо, ДанныеКонтрагента) Экспорт

	// _Демо начало примера
	
	Если ДанныеКонтрагента.ДополнительныеПараметры.Свойство("ЮридическоеФизическоеЛицо") Тогда
		ЮрФизЛицо = ДанныеКонтрагента.ДополнительныеПараметры.ЮридическоеФизическоеЛицо;
		ЭтоЮрЛицо = (ЮрФизЛицо = Перечисления._ДемоЮридическоеФизическоеЛицо.ЮридическоеЛицо);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	// _Демо конец примера
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеИнформацииДляДосьеКонтрагента
	
// Заполнение ссылки на контрагента по данным информации о нем для отчета "Досье контрагента".
//
// Параметры:
//  Контрагент  - ОпределяемыеТипы.Контрагент - ссылка на контрагента, которая должна быть заполнена.
//  ИнформацияОСвязях  - Структура - описание см. ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхЮридическогоЛица
//                 и ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхПредпринимателя.
//
Процедура ЗаполнитьКонтрагентаПоИнформацииОСвязях(Контрагент, ИнформацияОСвязях) Экспорт

	// _Демо начало примера
	
	Запрос = Новый Запрос;
	Если ИнформацияОСвязях.Свойство("КПП") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник._ДемоКонтрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН
		|	И (Контрагенты.КПП = &КПП ИЛИ Контрагенты.КПП = """")";
		Запрос.УстановитьПараметр("КПП", ИнформацияОСвязях.КПП);
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Контрагенты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник._ДемоКонтрагенты КАК Контрагенты
		|ГДЕ
		|	Контрагенты.ИНН = &ИНН";
	КонецЕсли;
	Запрос.УстановитьПараметр("ИНН", ИнформацияОСвязях.ИНН);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Контрагент = Выборка.Ссылка;
		
	КонецЕсли;

	// _Демо конец примера
	
КонецПроцедуры

// Заполнение таблицы расчетных счетов контрагента для вывода в отчет "Досье контрагента".
//
// Параметры:
//  РасчетныеСчета  - ТаблицаЗначений - заполняемая таблица. 
//                 Структуру таблицы см. Отчеты.ДосьеКонтрагентов.НоваяТаблицаРасчетныеСчета.
//  Контрагент  - ОпределяемыеТипы.Контрагент - ссылка на контрагента из информационной базы.
//  ИнформацияОСвязях  - Структура - описание см. ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхЮридическогоЛица и
//                 ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхПредпринимателя.
//
Процедура ЗаполнитьРасчетныеСчетаИнформацииОСвязяхКонтрагента(РасчетныеСчета, Контрагент, ИнформацияОСвязях) Экспорт
	
	// _Демо начало примера
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.Код КАК Код,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.Банк.Представление КАК Банк,
	|	БанковскиеСчета.Банк.Код КАК БИК,
	|	БанковскиеСчета.Банк.КоррСчет КАК КоррСчет
	|ИЗ
	|	Справочник._ДемоБанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТаблицы Из ТаблицаРезультата Цикл
		
		НоваяСтрока = РасчетныеСчета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		
	КонецЦикла;
	
	// _Демо конец примера
	
КонецПроцедуры

// Заполнение таблицы контактных лиц контрагента для вывода в отчет "Досье контрагента".
//
// Параметры:
//  КонтактныеЛица  - ТаблицаЗначений - заполняемая таблица. 
//                 Структуру таблицы см. Отчеты.ДосьеКонтрагентов.НоваяТаблицаКонтактныеЛица.
//  Контрагент  - ОпределяемыеТипы.Контрагент - ссылка на контрагента из информационной базы.
//  ИнформацияОСвязях  - Структура - описание см. ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхЮридическогоЛица и
//                 ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхПредпринимателя.
//
Процедура ЗаполнитьКонтактныеЛицаИнформацииОСвязяхКонтрагента(КонтактныеЛица, Контрагент, ИнформацияОСвязях) Экспорт
	
	// _Демо начало примера
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КонтактныеЛица.Ссылка КАК Ссылка,
	|	КонтактныеЛица.Наименование КАК Представление
	|ИЗ
	|	Справочник._ДемоКонтактныеЛицаПартнеров КАК КонтактныеЛица
	|ГДЕ
	|	КонтактныеЛица.Владелец = &Партнер
	|
	|УПОРЯДОЧИТЬ ПО
	|	Представление";
	Запрос.УстановитьПараметр("Партнер", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер"));
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРезультата.Количество() > 0 Тогда
		
		МассивЛиц = ТаблицаРезультата.ВыгрузитьКолонку("Ссылка");
		ТаблицаКИ = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивЛиц);
		Для каждого СтрокаТаблицы Из ТаблицаРезультата Цикл
			
			НоваяСтрока = КонтактныеЛица.Добавить();
			НоваяСтрока.Ссылка        = СтрокаТаблицы.Ссылка;
			НоваяСтрока.Представление = СтрокаТаблицы.Представление;
			СтрокиКИ = ТаблицаКИ.Скопировать(Новый Структура("Объект", НоваяСтрока.Ссылка), "Представление,Тип");
			СтрокиКИ.Сортировать("Тип УБЫВ");
			Для каждого СтрокаКИ Из СтрокиКИ Цикл
				Если ЗначениеЗаполнено(СтрокаКИ.Представление) Тогда
					НоваяСтрока.Представление = НоваяСтрока.Представление + ", " + СтрокаКИ.Представление;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;

	// _Демо конец примера
	
КонецПроцедуры

// Заполнение почтового адреса контрагента по данным информации о нем для вывода в отчет "Досье контрагента".
//
// Параметры:
//  ПочтовыйАдрес  - Строка - заполняемый адрес. 
//  Контрагент  - ОпределяемыеТипы.Контрагент - ссылка на контрагента из информационной базы.
//  ИнформацияОСвязях  - Структура - описание см. ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхЮридическогоЛица и
//                 ДанныеЕдиныхГосРеестров.НоваяИнформацияОСвязяхПредпринимателя.
//
Процедура ЗаполнитьПочтовыйАдресИнформацииОСвязяхКонтрагента(ПочтовыйАдрес, Контрагент, ИнформацияОСвязях) Экспорт

	// _Демо начало примера
	
	ПочтовыйАдрес = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(Контрагент, 
		Справочники.ВидыКонтактнойИнформации._ДемоАдресКонтрагента);
	
	// _Демо конец примера
	
КонецПроцедуры

// Заполнение наименований видов деятельности по ОКВЭД для вывода в отчет "Досье контрагента".
//
// Параметры:
//  ВидыДеятельности  - ТаблицаЗначений - см. ДанныеЕдиныхГосРеестров.НоваяТаблицаВидыДеятельности.
//                 В процедуре необходимо заполнить поле НаименованиеОКВЭД строк таблицы по заполненному полю КодОКВЭД.
//
Процедура ЗаполнитьНаименованияВидовДеятельностиКонтрагента(ВидыДеятельности) Экспорт
	
КонецПроцедуры

#КонецОбласти 

#Область ПрочиеПроцедуры

// Процедура - Определение цветов выделения результатов проверки контрагентов.
// 		Для примера см. функцию ЦветаРезультатовПроверки ОМ ПроверкаКонтрагентовКлиентСервер.
//
// Параметры:
//  Цвета	 - Структура - Имена ключей - это названия цветов, которые необходимо определить.
// 		Список ключей см ПроверкаКонтрагентовВызовСервераПовтИсп.ЦветаРезультатовПроверки().
Процедура ОпределитьЦветаРезультатовПроверки(Цвета) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти