// Ожидаются параметры:
//
//     ИдентификаторОсновнойФормы      - УникальныйИдентификатор - Идентификатор формы, через хранилище которой
//                                                                 происходит обмен.
//     АдресСхемыКомпоновки            - Строка - Адрес временного хранилища схемы компоновки, для которой
//                                                редактируются настройки.
//     АдресНастроекКомпоновщикаОтбора - Строка - Адрес временного хранилища редактируемых настроек компоновщика.
//     ПредставлениеОбластиОтбора      - Строка - Представление для формирования заголовка.
//
// Возвращается результатом выбора:
//
//     Неопределено - Отказ от редактирования.
//     Строка       - Адрес временного хранилища новых настроек компоновщика.
//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОсновнойФормы = Параметры.ИдентификаторОсновнойФормы;
	
	КомпоновщикПредварительногоОтбора = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикПредварительногоОтбора.Инициализировать( 
		Новый ИсточникДоступныхНастроекКомпоновкиДанных(Параметры.АдресСхемыКомпоновки) );
		
	АдресНастроекКомпоновщикаОтбора = Параметры.АдресНастроекКомпоновщикаОтбора;
	КомпоновщикПредварительногоОтбора.ЗагрузитьНастройки(ПолучитьИзВременногоХранилища(АдресНастроекКомпоновщикаОтбора));
	УдалитьИзВременногоХранилища(АдресНастроекКомпоновщикаОтбора);
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Правила отбора ""%1""'"), Параметры.ПредставлениеОбластиОтбора);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Модифицированность Тогда
		ЗначениеВыбора = АдресНастроекКомпоновщикаОтбора();
		Если ЗначениеВыбора = Ложь Тогда
			Возврат;
		КонецЕсли;
		ОповеститьОВыборе(ЗначениеВыбора);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АдресНастроекКомпоновщикаОтбора()
	Если Не ПроверитьРекурсивно(КомпоновщикПредварительногоОтбора.Настройки.Отбор.Элементы) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(КомпоновщикПредварительногоОтбора.Настройки, ИдентификаторОсновнойФормы)
	
КонецФункции

&НаСервере
Функция ПроверитьРекурсивно(КоллекцияЭлементовОтбораКД)
	Результат = Истина;
	Для Каждого ЭлементКД Из КоллекцияЭлементовОтбораКД Цикл
		Тип = ТипЗнч(ЭлементКД);
		Если Тип = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементКД.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено
				Или ЭлементКД.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Недопустимый вид сравнения ""%1""'"), ЭлементКД.ВидСравнения);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , "КомпоновщикПредварительногоОтбора.Настройки.Отбор");
				Результат = Ложь;
			КонецЕсли;
		ИначеЕсли Тип = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Если Не ПроверитьРекурсивно(ЭлементКД.Элементы) Тогда
				Результат = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

#КонецОбласти

