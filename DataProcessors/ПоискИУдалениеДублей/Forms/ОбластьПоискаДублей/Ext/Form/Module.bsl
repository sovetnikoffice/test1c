// Ожидаются параметры:
//
//     ОбластьПоискаДублей - Строка - Полное имя метаданных таблицы ранее выбранной области поиска.
//
// Возвращается результатом выбора:
//
//     Неопределено - Отказ от редактирования.
//     Строка       - Адрес временного хранилища новых настроек компоновщика.
//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ОбластьПоискаДублей", ОбластьПоУмолчанию);
	
	ИнициализироватьСписокОбластейПоискаДублей();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбластиПоискаДублейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПроизвестиВыбор(ВыбраннаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ПроизвестиВыбор(Элементы.ОбластиПоискаДублей.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроизвестиВыбор(Знач ИдентификаторСтроки)
	
	Элемент = ОбластиПоискаДублей.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если Элемент = Неопределено Тогда
		Возврат;
		
	ИначеЕсли Элемент.Значение = ОбластьПоУмолчанию Тогда
		// Не было изменений
		Закрыть();
		Возврат;
		
	КонецЕсли;
	
	ОповеститьОВыборе(Элемент.Значение);
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСписокОбластейПоискаДублей()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбластиПоискаДублей = ОбработкаОбъект.ОбластиПоискаДублей(Ложь);
	
	ОбластиПоискаДублей.СортироватьПоПредставлению();
	Элемент = ОбластиПоискаДублей.НайтиПоЗначению(ОбластьПоУмолчанию);
	Если Элемент <> Неопределено Тогда
		Элементы.ОбластиПоискаДублей.ТекущаяСтрока = Элемент.ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти