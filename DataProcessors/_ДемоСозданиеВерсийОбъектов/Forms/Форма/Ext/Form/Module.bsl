
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сгенерировать(Команда)
	Прогресс = 0;
	Элементы.Прогресс.МаксимальноеЗначение = КоличествоВерсий;
	ПодключитьОбработчикОжидания("СгенерироватьПорцию", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВерсииОбъекта(Команда)
	ОчиститьРегистрНаСервере(ВерсионируемыйОбъект);
	ПоказатьПредупреждение(, НСтр("ru = 'Очистка завершена'"));
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРегистр(Команда)
	ОчиститьРегистрНаСервере();
	ПоказатьПредупреждение(, НСтр("ru = 'Очистка завершена'"));
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СгенерироватьПорцию()
	РазмерПорции = 5000;
	Если РазмерПорции > КоличествоВерсий - Прогресс Тогда
		РазмерПорции = КоличествоВерсий - Прогресс;
	КонецЕсли;
	СгенерироватьНаСервере(ВерсионируемыйОбъект, РазмерПорции, ДатаВерсий);
	Прогресс = Прогресс + РазмерПорции;
	
	Если Прогресс < КоличествоВерсий Тогда
		ПодключитьОбработчикОжидания("СгенерироватьПорцию", 0.1, Истина);
	КонецЕсли
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СгенерироватьНаСервере(Ссылка, Количество, ДатаВерсий)
	НомерПоследнейВерсии = ВерсионированиеОбъектов.НомерПоследнейВерсии(Ссылка);
	АвторВерсии = Пользователи.АвторизованныйПользователь();
	ДанныеОбъекта = СериализоватьОбъект(Ссылка.ПолучитьОбъект());
	ВерсияОбъекта = Новый ХранилищеЗначения(ДанныеОбъекта, Новый СжатиеДанных(9));
	
	НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
	Для Счетчик = 1 По Количество Цикл
		НомерВерсии = НомерПоследнейВерсии + Счетчик;
		
		Запись = НаборЗаписей.Добавить();
		Запись.Объект = Ссылка;
		Запись.НомерВерсии = НомерВерсии;
		Запись.ДатаВерсии = ДатаВерсий;
		Запись.АвторВерсии = АвторВерсии;
		Запись.ТипВерсииОбъекта = Перечисления.ТипыВерсийОбъекта.ИзмененоПользователем;
		Запись.ВерсияОбъекта = ВерсияОбъекта;
	КонецЦикла;
	НаборЗаписей.Записать(Ложь);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СериализоватьОбъект(Объект)
	ЗаписьXML = Новый ЗаписьFastInfoset;
	ЗаписьXML.УстановитьДвоичныеДанные();
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписатьXML(ЗаписьXML, Объект, НазначениеТипаXML.Явное);
	Возврат ЗаписьXML.Закрыть();
КонецФункции

&НаСервереБезКонтекста
Процедура ОчиститьРегистрНаСервере(Объект = Неопределено)
	НаборЗаписей = РегистрыСведений.ВерсииОбъектов.СоздатьНаборЗаписей();
	Если Объект <> Неопределено Тогда
		НаборЗаписей.Отбор.Объект.Установить(Объект);
		НаборЗаписей.Прочитать();
		НаборЗаписей.Очистить();
	КонецЕсли;
	НаборЗаписей.Записать();
КонецПроцедуры

#КонецОбласти


